  D-KHTM.APR : a99-Anzeige incl. verkn. Satzstrukturen, tabellarisch
  2002-02-15  Plus Nachladungen von ueber- und untergeordneten Saetzen
  Wenn keine hierarchischen oder verknuepften Saetze vorkommen,
  dann genuegt einfaches Modell  D-SHTM.APR  
  ALFA: Unterprogramm #(c, ruft Javascript  ausl(), rtrn(), vorm() in av_func.js,
              dieses aktiviert  alfa.php, welches die Jobs enthaelt. 
        wenn nicht gewuenscht, dann kommentierte Zeile in d-k.apt deaktivieren

pn="Hierarchie-Export HTML"

  Fuellen Sie die Bereiche zwischen den mit <<<<<<<<<  und >>>>>>>>>
  markierten Zeilen mit Ihren Exportbefehlen aus!
  Meistens reicht es, den Abschnitt unter Sprungmarke #-Z anzupassen!
  (siehe ganz unten)

  Der aktuelle (anzuzeigende oder auszugebende) Satz wird PRIMAERSATZ genannt.
  Dabei handelt es sich um entweder

  A.  einen Hauptsatz
      wenn er unabhaengig gespeicherte Untersaetze hat, muss die
      Kategorie #00 mit  einer eindeutigen IdNr X besetzt sein.
      In der Anzeige sollen unter dem Hauptsatz alle untergeordneten
      Saetze erscheinen.
      Die untergeordneten Saetze haben dann in #00 IdNummern der Form  X+n
      mit einer Bandnummer n :

  B.  einen Untersatz
      dann enthaelt #00 die IdNr des Hauptsatzes PLUS eine Bandnummer
      z.B.  #00 123456+005=Vol.5
      (hinter '='  kann eine Bandnummer in Textform folgen, sonst wird
      die auf '+' folgende Nummer genommen, fuehrende Nullen werden beseitigt)
      In der Anzeige sollen stufenfoermig die unmittelbar uebergeordneten, 
      aber nicht die nebengeordneten Saetze erscheinen.

  C.  beides, auch "Mittelsatz" genannt: der Primaersatz ist einerseits
      einem anderen untergeordnet, besitzt aber auch selbst Untersaetze.
      In der Anzeige soll fuer die untergeordneten Saetze nach A, fuer die
      uebergeordneten nach B verfahren werden.


  Es kann bis zu 5 untergeordnete Stufen geben (theoretisch noch mehr).
  Jede weitere Stufe enthaelt in der #00 oder #09 ein weiteres '+' . 
  Dieses wird fuer den Nachladevorgang ausgewertet.

  *********** Grundparameter, nicht ohne Grund aendern: ***************
td-html      HTM-Steuerbefehle
         xxx Aktivieren, wenn intern ASCII, extern UTF-8
p & "&amp;"
p < "&lt;"
p .34 "&quot;"

tad-utf     ASCII->ANSI Codetabelle

zl=0     Zeilenlaenge: es wird kein Zeilenumbruch gemacht
ze=" " 13 10
zb=" -"   Umbruch bei Leerz. und Bindestrich
zm=0      keine Seitenschaltung
dx=1      Umcodierung einschalten
ae=""    </table>
i4=1    V14-Ersetzungen ausfuehren

  <<<<<<<<<<<<    Raum fuer Ihre Zwischenteile (Textkonstanten)
  Empfehlung: Nummern 30 bis 59
  >>>>>>>>>>>>    Ende der anwendungsspezifischen Textkonstanten
4="<br> "

2="; "

  Tabellen-Parameter                  erste Spalte:
18='<table width="100%">'         schmal
17='<table width="100%">'       breit
19=""
16="<tr><td>--- </td><td>--- </td></tr>"   Ende eines Satzes

13="<tr><td>&nbsp;</td><td>&nbsp;</td></tr>"
20=C "<tr><td valign=top><i>"     Neue Tabellenzeile
21="</i></td><td>"                zwischen zwei Tab.Spalten
22="</td></tr>" C                 Ende der Tab.Zeile
23="<br>"                     Vorschub innerhalb Tab.Zelle

    nur bei HTML:
29="<pre>"
28="</pre>"


p ª 1     Nichtsortierzeichen ausblenden
      Absatzende
p  "<br> "

#uII +( e0

#hi +#99x e0
#nr "1" e1 =sP e0
#99x

#t{ "<table>" }

td-k          d-k.apt laden


   <<<<<<<<< Unterprogramme hier anfuegen, wenn man welche hat <<<<<<<
   Unterprogramm fuer URL

  Titeldaten bearb. (spaeter)
#(A
#t{ "" }
#)A

#(U
#t{ t20 "URL:     " t21 } 
#cc e$ y0 C p'<a href="' P'" target="_blank">'
#cc B$u B"\31?" f" " e$ y1 P"</a>"

#)U

#(u
#cc +- i4,i i5,s i6,i e0
#t{ t20 " --     " t21 } 
#cc e$ y0 C p'; <a href="' P'" target="_blank">'
#cc B$u B"\31?" f" " e$ y1 P"</a>"

#)u

   ALFA
#(c
        Diese Zeile aktivieren, wenn ALFA gewuenscht

#cc +#J1 I3,  e0   #90x
#usf +#J2 e0       #90

#J1

 #+%    (falls ALFA stets gesetzt sein soll, ohne #uAL zu pruefen)

#uAL +% e0   ALFA? dann #-%
#cc e" = " ,"_\31L__" ,"_\31s_:_" e$ e" = " p{ t20 "Signatur: " t21 t79 t66 } P{ t98 t111 t22 }

#cc $D y0 p" <font color=red> <i>ausgeliehen!</i></font>" e"!"

#uAU +#cc e0   Ausleihangabe nur zeigen, wenn #uAU gesetzt
#J2
#+-

#cc $u p{ t20 "ausgeliehen an: " t21 } P22
#)c

#-%
#t{ s1 }    um die Kat.Nummer auswerten zu koennen
#cc +#90A $u e0    teilfeld u vorh. -> Rueckgabe
#cc +#90B $p e0    keine Ausl., wenn $p

   Zur Differenzierung falls gleiche Signaturen
 #cc b3 e1 { C "\qr " t69 "Ausleihe " }
 #cc e$ b4 P68
   statt dieser Zeile:
 #uAU +#90@ e0
 #cc +#uci e$ e" = " b4 p{ t20 "Signatur: " t21 '<a href="grec.php?urN=' } e0
#90@
#t{ C t20 "Signatur:   " t21 C '<span id="s' }
#usP b3
#t{ '">[ausleihbar]</span> ' } 
#cc p{ '<a href="javascript:ausl(' 39 'urN=' } e0

  hierher auch von Rueckgabefktion um den Link zu komplettieren
#-q
 #nra +#cc b3
 #nr b3
#99z +#cc b3
#nr b3
  KatNr d. Sign. oder Zgnr
#cc e3 F" " "','usI="
#t{ s4 }
  zaehler
#usP "','usP=s"
  #cc p'x var "|;Ausl. " P"an ?=" #uuS\ask\if "" end\yesno Ausleihen?\if n end\if c end\ins #uuS\var #uuS(e"=")\ins $u-!' e"!" =ci
 #nr p"x var ''\ins #usn\!" e"!" =ci
#t{ s1 }
  BandNr
#01 "','usN="
#02 "','usN="
#03 "','usN="
#04 "','usN="
#t{ s4 }

#t{ 39 ')" id="s' }
#usP P"a"
#t{ '">' }   beendet <a... 
  jetzt die Sign, / ZgNr
#cc ,"_\31L__" ,"_\31s_:_" e$ e" = "
#t{ '</a>' }
  Zaehler
#usP x"+1" e"." =sP
#)c

#90A e0
 #uAU +#90C e0   Rueckgabe nur zeigen, wenn #uAU gesetzt
 #nr +#90B e0
   Zur Differenzierung falls gleiche Signaturen
 #cc b3 e1 { C "\qr " t69 "Ausleihe " }
 #cc e$ b4 P68

#90C e0
#t{ C t20 "Signatur:   " t21 '<span id="s' }
#usP b3
#t{ '">RÃ¼ckgabe </span> ' } 
#cc p{ '<a href="javascript:rtrn(' 39 'urN=' } e0
#+q

#90B e0
#cc e$ e" = " b4 { t20 "Signatur: " t21 }
#t{ s4 }
#uaU +#90C e0
#cc $u p" (ausgeliehen)!" e"!"

#90C e0
#t22

#)c

#(C

#t{ t72 "javascript:reqRes('|9 " }
#9DA $T e"+" P"?"
#t{ "')" t104 "Gesamtdaten anzeigen" t97 }
#)C

#(D

#9DG $Y B"/" { " ... " t72 t66 "Neues Heft (zu Bd." } P{ ")" t98 t104 }
 #9DG $Y B"/" p "Neues Heft (zu Bd." P")" =Y~
 #00 p"x f1nd ppn " P"\dis\sho rec\exec z-heft" =Z~
 #9DA $J "x f1nd ppn " P"\dis\sho rec\exec z-heft" =Z~

#)D



  verkn. Untersatz in der Anzeige
#(E

#t{ "   " t72 "javascript:reqRes('|9 " }
#09 +#J1 c"+" e"+" P"?"
#00 e"+"
#J1 e0 
#t{ "')" t104 "Gesamtdaten anzeigen" t97 }


#nr e0
#)E



  Internformat-Anzeige:  (a99/alcarta F5)
   Achtung: es darf nicht  ke=""  gesetzt sein, dann geht's nicht
#-(
#bze=C
#bks=0
#bi4=-1        V14 aus : keine V14-Ersetzungen
#t{ s0 }

##
#bke=C
#t{ s4 }       oder 5 statt 4 wenn 3stelliges Schema!
#bi4=1         V14 wieder ein

#+#

  f. mehrteilige
#(J

#t{ "   " t72 "javascript:reqRes('|9 " }
#09 +#nr c"+" e"+" P"?"
#00 e"+" P"?"
#nr e0 
#t{ "')" t104 "Hierarchie-Liste" t97 }

#)J



  Exemplarsachen u.a.
#(N
 
#9B +#9B e0
#9DG +#9DI i4,V e0

#9DF e$ p{ '<a href="javascript:reqRes(' 39 "|9 " } P{ 39 ');">[Titelsatz zeigen]</a>' }
#t4


 #uzp +#99Z c"alc" e0      OPAC: kein Kommentar
 #9DF p"x form Exempl\if ca end\var #ubs\ins #9DB\yesno Speichern?\if yes put\if cancel undo\!" e"!" =Z~
 #t{ "   " t72 "Exemplardaten bearbeiten" t104 C }
 #9DF "Exemplardaten bearbeiten!" e"!" =Y~

#99Z
#)N

#9B e0
#t{ t20 "Ausweisnr.: " t21 } 
#9B e$ b1 
#t22
#<
#9DI +#9B $u "B" |:0
#)N
#9B e$ b1 { t20 "Vor-Entleiher :" t21 } P22
#<

#)N

  Inventarisierung etc.
#(P

#9DA $T p{ '<a href="javascript:reqRes(' 39 "|9 " } P{ 39 ');">[Titelsatz zeigen]</a>' }

 #9DB +#9Dx I4,3 I4,4 e0
 #t{ t72 "Bestellung modifizieren" t104 }
 #9DA "Bestellung modifizieren!" e"!" =Y~
 #9DA $T p"x form Bestell\if ca end\depos D\var #ubs\ins #9DB\yesno Speichern?\if yes put\if cancel undo\!" e"!" =Z~

#9Dx
  ORDER bzzw. ZAboM,  ToDo
#9DB +#9Dy i4,6 e0
#9DB +#9Dy i4,7 e0
#9DB +#9Dz i4,A e0   Abo
#9DB +#9Db i4,B e0   Band

 #9DA $T p"X o-invent.flx!" e"!" =Z~
 #t{ t72 "Inventarisierung" t104 }
 #9DA +#9Dy "Inventarisierung!" e"!" =Y~
#9Dz
 #9DA $T p"X o-func.flx!" e"!" =Z~
 #t{ t72 "Abo-Funktionen" t104 }
 #9DA +#9Dy "Abo-Funktionen!" e"!" =Y~
#9Db
 #9DA $T p'x var "|9 " #9DA$J\find\shorec\dis!' e"!" =Z~
 #t{ t72 "Abo-Daten" t104 }
 #9DA +#9Dy "Abo-Daten!" e"!" =Y~

#9Dy

#)P


#-S
#t{ t20 "Themen:" t21 }
#nr p"?|3!" e"!" =pf
#31 p>s e0
#31s >s e0
#31g >s e0
#33B >s e0
#33L >s e0
#nr p"?|2" e3 =pf
#31c >s e0
#31k >s e0
#nr p"x ind per " e10 =pf
#31p >s e0
#36 >s e0
#31t >s e0
#35 ++ p{C t79 "Werk-SW : " #250 t111} m{C} P>X Z
 #34 ++ p{C t79 "Sonder-SW : " #250 t111} m{C}
#t{#0}
#t22
 #31. ++ e"_" p{ t20 "Themen:  " t21 } m{ t23 }
 #t22
 #33s { t20 "FremdSchlagw.:  " t21 } P22
#+T

#(s   Schlagw.
#cc e"_" =sw

#-&
#upf =sW
#usw u e";" F" " ,"_: _ / _" ,"_\170__" AsW    Bef. wird zus.gesetzt

#usw C e";" e"_" F" " p{ t72 "javascript:reqInd('|3 " } P{ "')" t104 }
#usw e";" e"_"
#t{ t97 "; " }
#usw +& b";" f" " =sw

#)s


#(T

#t{ t20 "   " t21 }
#nr p{ '<a href="javascript:reqRes(' 39 "|9 " } e0
#00 e"+" P"!:~?"   code for +
#nr p{ 39 ");" 34 ">Liste der Themenhefte</a>" } e0
#t22

#)T



#(W
#87 e" =" C p{ t20 "   " t21 '<a href="http://www.librarything.com/isbn/' } P{ '">LibraryThing</a>' t22} 
#87 e" =" C p{ t20 "   " t21 '<a href="http://www.worldcat.org/isbn/' } P{ '">OCLC WorldCat</a>' t22} 
  Google Booksearch: entweder mit Logo,
 #87 +#J1 e" =" C p{ t20 "   " t21 '<a href="http://books.google.de/books?q=isbn%3A' } P{ '"><img src="gbs.gif"></a>' t22 } 
 #20 u e" :" C p{ t20 "   " t21 '<a href="http://books.google.de/books?q=intitle%3A%22' } P{ '%22"><img src="gbs.gif"></a>' t22 } 
  oder mit Text
#87 +#J1 e" =" C p{ t20 "   " t21 '<a href="http://books.google.de/books?q=isbn%3A' } P{ '">Google Booksearch</a>' t22 } 
#20 u e" :" C p{ t20 "   " t21 '<a href="http://books.google.de/books?q=intitle%3A%22' } P{ '%22">Google Booksearch</a>' t22 } 
#J1
#)W

  UP f. Exemplare
#(X
  #+-    falls nicht gewuenscht
#nr drA dvF =nr e0
#nra =nr e0
     #uvF = Verfuegbarkeits-Flag
#00 +#J1 P":" |:2    erstes laden
#+-    es gibt keins
#J1
#t{ t20 "Exemplare: " t21 }
#nr +#9DG e0
#00 P":E" |102
#-:
#9DG +#J3 i4,V e0
#9DG $e P". "
#9DG $s e" ="

#9DG +#J4 i4,W " momentan reserviert !" e"!"
#9DG +#J2 i4,v " Verlust !" e"!"
#9DG +#J2 $v c"p" " Pr„senz !" e"!"
#9DH +#9DH P{ t73 " ausgeliehen bis " t105 } e0
  es gibt verfuegb. Ex., Flag setzen
#dt +#J2 =vF e0
#9DH $R b6 e2 P"-" 
#9DH $R b4 e2 P"-" 
#9DH $R e4 
#J4
#nra =rA

#J2
#t23
#J3
#<
#00 +: P":E" |103

#t{ "</td></tr></table>" }

  Vormerk-Flip nur, wenn kein Ex verfuegbar.
  vorm() in  av_func.js bzw. a35alf.js  startet a-ovorm.job 
#uvF +#J5 e0

#00 p{ ' <a href="javascript:vorm(' 39 } P{ 39 ')">Vormerken</a>' t23 }
#J5

#)X

#(Y
#9GG p{ "Geb&uuml;hrensatz" } e0 
#3n i4,s I5,p I4,s { "Schlagwort-Stammsatz" } e0
#t4
#t{ ' [<a href="javascript:reqJob(' "'cfga');" '">Details</a>]' } 
#)Y
   >>>>>>>>> Ende der Unterprogramme >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

