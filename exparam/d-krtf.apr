  d-krtf.apr : a99-Anzeige incl. verkn. Satzstrukturen, tabellarisch
  2002-02-15 / 2016-05-20   mit ZAboM und ALFA
  Plus Nachladungen von ueber- und untergeordneten Saetzen
  Wenn keine hierarchischen oder verknuepften Saetze vorkommen,
  dann genuegt einfacheres Modell:  d-srtf.apr
  Pendant fuer HTML s. d-khtm.apr
  2014-01: "kritische Zeichen" eliminiert, auch in d-k.apt

pn="Hierarchie-Export RTF" 

  Fuellen Sie die Bereiche zwischen den mit <<<<<<<<<  und >>>>>>>>>
  markierten Zeilen mit Ihren Exportbefehlen aus!

  Der aktuelle (anzuzeigende oder auszugebende) Satz wird PRIMAERSATZ genannt.
  Dabei handelt es sich um entweder

  A.  einen Hauptsatz
      wenn er unabhaengig gespeicherte Untersaetze hat, muss die
      Kategorie #00 mit  einer eindeutigen IdNr X besetzt sein.
      In der Anzeige sollen unter dem auptsatz alle untergeordneten
      Saetze erscheinen.
      Die untergeordneten Saetze haben dann in #00 IdNummern der Form  X+n
      mit einer Bandnummer n :

  B.  einen Untersatz
      dann enthaelt #00 die IdNr des Hauptsatzes PLUS eine Bandnummer
      z.B.  #00 123456+005=Vol.5
      (hinter '='  kann eine Bandnummer in Textform folgen, sonst wird
      die auf '+' folgende Nummer genommen, fuehrende Nullen werden beseitigt)
      In der Anzeige sollen stufenfoermig die unmittelbar uebergeordneten,
      aber nicht die nebengeordneten Saetze erscheinen.

  C.  beides, auch "Mittelsatz" genannt: der Primaersatz ist einerseits
      einem anderen untergeordnet, besitzt aber auch selbst Untersaetze.
      In der Anzeige soll fuer die untergeordneten Saetze nach A, fuer die
      uebergeordneten nach B verfahren werden.


  Es kann bis zu 5 untergeordnete Stufen geben (theoretisch noch mehr).
  Jede weitere Stufe enthaelt in der #00 oder #09 ein weiteres '+' .
  Dieses wird fuer den Nachladevorgang ausgewertet.

  *********** Grundparameter, nicht ohne Grund aendern: ***************
td-rtff   RTF-Steuerbefehle: Codes f. Attribute und Schriften
tad-aw    oder utf-rtf.apt : Unicode -> RTF (wenn intern UTF-8)

zl=0   Zeilenlaenge, nicht besonders wichtig!
         denn es wird kein Zeilenvorschub gemacht:
ze=" "  
zb=" -"   Umbruch bei Leerz. und Bindestrich
zm=0      keine Seitenschaltung
dx=1      Umcodierung einschalten

  ae="</table>"   fuer HTML aktivieren

i4=1    V14-Ersetzungen ausfuehren

  <<<<<<<<<<<<    Raum fuer Ihre Zwischenteile (Textkonstanten)
  Empfehlung: Nummern 30 bis 59
  >>>>>>>>>>>>    Ende der anwendungsspezifischen Textkonstanten
4="\par "

  Tabellen-Parameter RTF              erste Spalte:
18="\pard\uc1\tx2272\li1800\fi-1800\f4\fs24 "       breit
19="\pard\uc1\tx2272\li1800\fi-1800\f4\fs24 "         schmal
   \f6 = Schrift 6 = Verdana.  \f0 einstellen, wenn Sonderzeichen noetig

  18=""   fuer HTML
  19="<table>"   fuer HTML

13="\par "
20="\par\i\ "     Neue Tabellenzeile
21="\i0\tab\ "      zwischen zwei Tab.Spalten
22=""            Ende der Tab.Zeile (bei RTF nichts)
23=t20 t21
    nur bei RTF:
29="\f1 "


p .20 "\par \tab "      Absatzende
p .170 1     Nichtsortierzeichen ausblenden
      RTF-Steuerzeichen maskieren:
p { "\{"
p } "\}"
p \ "\\"

  Hier gehts los

#u}D +C e0
  Stellt die Schriftgroesse ein: (D=Display; #u{X sind spezif. Variablen)
  wenn ein Default f.d. Schriftgroesse gewuenscht (sonst Wert \fs aus disphead.rtf)
#dt p"\fs24!" e"!" =}D e0


#-C       Fuer Einsprung vou aussen fuer Normalanzeige!
#u}D P{ C } y0

#hi +#uhi e0
#t{ "\f6\fs24 " }

#nr dY~ dZ~ e0

#uhi e0
td-k     d-k.apt laden  (das ist der Hauptteil, gilt f. HTML und RTF)
to
#+#

   <<<<<<<<< Unterprogramme hier anfuegen, wenn man welche hat <<<<<<<

   Abschnitt fuer die Intern-Anzeige mit Feldnummern (F5 in a99)
#-(
#uzp +C c"alc" e0

#bp .35 35    Texel
#bp & &    V23
#bi5=0
#bi4=0

#t{ "\pard\uc1\tx2272\li580\fi-580\fs24 "       breit
#nr =aF e0

#t29    Schrift 1
#t{ t0 }

#nr p"x" e2 =dm

#t{ s0 }       oder 5 statt 4 wenn 3stelliges Schema!

  Das erste Feld nehmen und ausgeben
#u01

  Schleife f. alle weiteren Felder
#-)
  Das naechste, wenn noch eins da ist!
#u02 +) p4

               oder
 #L            alternativ: Anzeige mit Labels aus der CFG
#t{ t4 }
#bks=04
#t28
#bp  "\par \tab "
#bp .35 8
#bi5=95
#bi4=1
#+#


  Unterprogramme, meist Flips oder Links
  ZAboM
#(A
#uzp +#00 c"alc" e0
#t{ " ... " t72 "Titeldaten bearb." t104 }
#00 "Titeldaten bearb.!" e"!" =Y~
#00 "x f1nd ppn " P"\dis\sho rec\exec onforms" =Z~
#)A

#(B
#9DG $Y { t20 "Jg./Bde." t21 t72 } P{ t104 t22 }
#9DG +#00 $Y =Y~
#)B
#00 e0
#</
#00 p"x var '|: " P"-al?'\f1nd\dis\sho rec" =Z~
#)B

  Titeldaten zum Zs-Bandsatz anzeigen
#(C
#t{ t72 t66 "Gesamtdaten anzeigen" t98 t104 }
#nr "Gesamtdaten anzeigen!" e"!" =Y~
#9DA $T "x f1nd ppn " P"\dis\sho rec" =Z~

#)C

#(D
#9DG $Y { t20 "Jg/Band:" t21 t72 t66 } P{ t98 t104 }
#9DG $Y =Y~
#00 p"x f1nd ppn " P"\dis\sho rec\exec o-func" =Z~

#9DG $Y B"/" { " ... " t72 t66 "Neues Heft (zu Bd." } P{ ")" t98 t104 }
#9DG $Y B"/" p "Neues Heft (zu Bd." P")" =Y~
#00 p"x f1nd ppn " P"\dis\sho rec\exec z-heft" =Z~
 #9DA $J "x f1nd ppn " P"\dis\sho rec\exec z-heft" =Z~

#t{ " ... " t72 t66 "Umlaufende Hefte" t98 t104 }
#nr p"Umlaufende Hefte!" e"!" =Y~
#9DA $T "x find |: u-" P"/?\if l1 mes Momentan keine;end\ord d13\sho list" =Z~
#)D

  verkn. Untersatz
#(E
#t{ t72 t66 "Gesamtdaten anzeigen" t98 t104 }
#nr "Gesamtdaten anzeigen!" e"!" =Y~
#09 +#nr e"+" "x f1nd ppn " P"\dis\sho rec" =Z~
#00 e"+" "x f1nd ppn " P"\dis\sho rec" =Z~

#nr e0
#)E

#(F
#9DB e$ c"^Al" p" (nicht mehr lfd.)!" e"!"
#00 p{ " ... " t72 "Abodaten bearb." t104 } e0
#00 "Abodaten bearb.!" e"!" =Y~
#00 "x f1nd ppn " P"\dis\sho rec\form Abo" =Z~
#00 p{ " ... " t72 "Neuer Band" t104 } e0
#00 "Neuer Band!" e"!" =Y~
#00 "x f1nd ppn " P"\dis\sho rec\exec z-nband" =Z~
#)F

#(H
#9DA +# $$ c"V000" " (Umlauf beendet)!" e"!"      Umlauf schon beendet
#00 p{ " ... " t72 "bearbeiten" t104 } e0
#00 "bearbeiten!" e"!" =Y~
#00 "x f1nd ppn " P"\dis\sho rec\form Heftuml" =Z~
#00 p{ " ... " t72 "Heft weitergeben." t104 } e0
#00 "Heft weitergeben.!" e"!" =Y~
#00 "x f1nd ppn " P"\exec z-heft wg" =Z~
#)H

#(I
#8n u e" :" =st
#t{ t72 "Titel aus dieser Serie" t104 }

#cc p"Titel aus dieser Serie!" e"!" =Y~
#8n u e" :" p"x find |5 " P" ;?\sho list!" e"!" =Z~

#)I

  f. mehrteilige
#(J

#t{ "   " t72 "Hierarchie-Liste" t104 }
#09 +#nr e"+" p"x fin |9 " P"?\order a0\sho list" =Z~
#00 e"+" p"x fin |9 " P"?\order a0\sho list" =Z~
#nr p"Hierarchie-Liste!" e"!" =Y~

#)J


#(N
#9B +#9B e0
#9DG +#9DI i4,V e0
#9DF e$ p{ t72 "Titelsatz zeigen" t104 " (Nr. " } P")"
#9DF e$ p"x f1n |9 " P"\dis\sho rec" =Z~
#9DF p"Titelsatz zeigen!" e"!" =Y~

#uzp +#99Z c"alc" e0      OPAC: kein Kommentar
#9DF p"x form Exempl\if ca end\var #ubs\ins #9DB\yesno Speichern?\if yes put\if cancel undo\!" e"!" =Z~
#t{ "   " t72 "Exemplardaten bearbeiten" t104 C }
#9DF "Exemplardaten bearbeiten!" e"!" =Y~

#99Z
#)N
#9B e$ b1 { t4 "Ausweisnr.    :" t72 } P104
#9B e$ b1 =Y~
#9B e$ b1 "x f1nd |: B" P"\sho rec\dis" =Z~
#9DI +#9B $u "B" |:0
1#<
#)N
#9B e$ b1 { t4 "Vor-Entleiher :" t72 } P104
#9B e$ b1 =Y~
#9B e$ b1 "x f1nd |: B" P"\sho rec\dis" =Z~
#<

#)N

  Inventarisierung etc.
#(P
#9DA $T p"x f1n |9 " P"\dis\sho rec" =Z~
#t{ t72 "Titelsatz zeigen" t104 }
#9DA "Titelsatz zeigen!" e"!" =Y~
#9DB +#9Dx I4,3 I4,4 e0
#t{ t72 "Bestellung modifizieren" t104 }
#9DA "Bestellung modifizieren!" e"!" =Y~
#9DA $T p"x form Bestell\if ca end\depos D\var #ubs\ins #9DB\yesno Speichern?\if yes put\if cancel undo\!" e"!" =Z~

#9Dx

#9DB +#9Dy i4,6 e0
#9DB +#9Dy i4,7 e0
#9DB +#9Dz i4,A e0   Abo
#9DB +#9Db i4,B e0   Band

#9DA $T p"X o-invent.flx!" e"!" =Z~
#t{ t72 "Inventarisierung" t104 }
#9DA +#9Dy "Inventarisierung!" e"!" =Y~
#9Dz
#9DA $T p"X o-func.flx!" e"!" =Z~
#t{ t72 "Abo-Funktionen" t104 }
#9DA +#9Dy "Abo-Funktionen!" e"!" =Y~
#9Db
#9DA $T p'x var "|9 " #9DA$J\find\shorec\dis!' e"!" =Z~
#t{ t72 "Abo-Daten" t104 }
#9DA +#9Dy "Abo-Daten!" e"!" =Y~

#9Dy

#)P


#-S
#t{ t20 "Themen:" t21 }
#usS dsS e0
#t{ "\par \pard \fi1750 " }
#nr p"?|3!" e"!" =pf
#31 p>s e0
#31s >s e0
#31c >s e0
#31k >s e0
#31g >s e0
#33B >s e0
#33L >s e0
#nr p"?|2" e3 =pf
#31c >s e0
#31k >s e0
#nr p"x ind per " e10 =pf
#31p >s e0
#36 >s e0
#31t >s e0
#35 ++ p{C t79 "Werk-SW : " #250 t111} m{C} P>X Z
 #34 ++ p{C t79 "Sonder-SW : " #250 t111} m{C}
#t{#0}
#t22
 #31. ++ e"_" p{ t20 "Themen:  " t21 } m{ t23 }
 #t22
#33. ++ p>s m>s e0
#t22
#t4
#t18
#t4
#+T

#(s   Schlagw.
#cc =sw

#-&
#upf =sW
  Hilfsvar. als Trenner " ; " nach dem ersten SW
#usS { "\par \pard\fi1750 " } e0
#usw u e";" e"_" F" " ,"_: _ / _" ,"_\170__" AsW    Bef. wird zus.gesetzt
#usW 0 =Z~
#usw e";" e"_" F" " ,"_\170__" =Y~
#usw C e";" e"_" F" " p{ t72 } P{ t104 }
#nr 0 =sS e0
#usw +& b";" f" " =sw
#)s
  ZAboM: Anzeige der zur Zs gehoerigen Abos und Bde
  Typische Eintr.: Abo: |:zzz0081-999909 -Al ab 2007. Lief.: BR
                    Bd: |:zzz0081-999909 -Bl 7992=2008

#(T
#8n p{ t4 "   " t72 "Liste der Themenhefte" t104 } e0

#00 e"+" p"x find |9 " P"+?\if g0 sho list;end\var '|5 ' #8n ' ;?'\find\if g0 sho list;end;mes sorry" =Z~
#t4
#8n p"Liste der Themenhefte!" e"!" =Y~

#)T

#(Z
#t{ "\par " }
#00 dzi =zi e0
#9DA $T dzi =zi e0
  jetzt ist #uzi die TitelId. Gibt es einen Eintrag?
#uzi +#ux: p"zz" |:8
#)Z
#-=
#t{ "\par " }
  die gefundene Reg.Zeile
#ubo dbo e0
#ux: b" -al" p"lfd.Abo " =bo
#ux: b" -ae" p"alt.Abo " =bo
#ux: b" -ab" p"abb.Abo " =bo
#ux: b" -bl" B"=" p"akt.Bd " =bo
#ux: b" -bv" B"=" p"abg.Bd " =bo
#ux: b" -bu" B"=" p"unv.Bd " =bo
#ux: b" -a" P{"\fs22 "} e0
#ux: b" -b" P{"\fs18  -- "} e0
#ubo p72 P104
#ubo =Y~
  #ux: ist die gefundene Reg.Zeile
#ux: p'x f1nd |: "_' P'"\dis\sho rec' =Z~

  naechste Zeile
#uzi p"zz" |:9
#ux: += c"#uzi" e0
#)Z

   Unterprogramm fuer URL
#(U
#t{ t20 "URL: " t21 }
#cc +#cc e0
#(u
#cc +#8eZ i5,: e0
#t{ "\par \pard \fi1800 " }
#cc e$ ,"_\_/_" p'x var "' P'"\ins #ucc\exec winstart.flx' =Z~
 #cc e$ p"~start " =Z~
#cc B$u B"\31?" f" " =Y~
#cc B$u B"\31?" f" " p72 P104
#ucc dcc e0
#t22

#)u
#)U
#8eZ
#cc e$ p"X _access!" e"!" =Z~
#cc B$u B"\31?" f" " =Y~
#cc B$u B"\31?" f" " p72 P104
#t22
#t4
#)U
#)u

   ALFA Ausleihfunktionen: die anklickbaren Flips aufbereiten
#(c   FLEX fuer ALFA

#cc +#J1 I3,  e0   #90x
#usf +#J2 e0       #90 nur nehmen, wenn #usf nicht gesetzt

#J1
#uAL +Ë e0   ALFA? dann #-Ë
#cc ,"_\31s_:_" B$L ~e e$ e" = " p{ t20 "Signatur: " t21 t79 t66} P{t98 t111 ' \qr' C '\ql '}

#uAU +#cc e0   Ausleihangabe nur zeigen, wenn #uAU gesetzt
#J2
#+-

#cc $u p{ "ausgeliehen an: " t66} P{t98 ' \qr' C '\ql '}
#)c

#-Ë
#bp .35 35   # als Texel-Steuerzeichen abschalten!
#cc +#90u $u e0    Unterfeld u vorh. -> Rueckgabe
#cc +#90Å $p e0    keine Ausl., wenn $p

   Zur Differenzierung falls gleiche Signaturen
 #t{ s0 }    um die Kat.Nummer auswerten zu koennen
 #cc b3 e1 { C "\qr " t72 "Ausleihe " }
 #cc e$ b4 P104
 #t{ s4 }    um die Kat.Nummer auswerten zu koennen
   statt dieser Zeile:
#uAL +#90@ e0
#cc +#uci e$ e" = " b4 p"Signatur: " p{ C "\qr " }
#90@
#cc B$s e$ y1 e" =" { t4 "\qr " t72 "Ausleihe " } P104
#t{ s0 }    um die Kat.Nummer auswerten zu koennen
#nr p"x var ''\ins #usn\!" e"!" =ci
#01 "sub " P"\" Aci
#02 "sub " P"\" Aci
#03 "sub " P"\" Aci
#04 "sub " P"\" Aci
  nachgeladener Untersatz? Nr als #uci an ausl.flx mitgeben
#nra b4 p'var "' P'"\ins #usn\' Aci
#cc e4 p'var "' P'"\ins #usi\exec ausl' Aci
 entweder:
 #cc b3 e1 =yY          1.Zeichen zur Differenzierung bei gleicher Sign.
 #cc e$ y0 b4 AyY
 oder:
#t{ s4 }
#cc B$s e$ y0 =yY
#uyY y0 e" =" p"Ausleihe " =Y~

#uci =Z~
#uci dci e0

#8n P{ "  " t72 "Bd.Ausl." t104 } e0
#8n p"Bd.Ausl.!" e"!" =Y~
#8n p"X alfaband a!" e"!" =Z~

#9X P{ "  " t72 "Bd.Rckg." t104 } e0
#9X p"Bd.Rckg.!" e"!" =Y~
#9X p"X alfaband r!" e"!" =Z~

#bp .35 8    # wieder einschalten
#t{C "\ql " }
#)c

#90u e0    nur Spr.marke
#uAL +#cc e0   Rueckgabe nur zeigen, wenn #uAL gesetzt
#nr +#90Å e0
   Zur Differenzierung falls gleiche Signaturen
 #t{ s0 }    um die Kat.Nummer auswerten zu koennen
 #cc b3 e1 { C "\qr " t72 "R¸ckgabe " }
 #cc e$ b4 P104
 #t{ s4 }    um die Kat.Nummer auswerten zu koennen
   statt dieser Zeile:
#cc B$s e$ y1 { t4 "\qr " t72 "R¸ckgabe " } P104

#nr p"x yes R¸ckgabe?\if not yes end!" e"!" y0 =ci
#t{ s0 }    um die Kat.Nummer auswerten zu koennen
#01 "\sub " Aci
#02 "\sub " Aci
#03 "\sub " Aci
#04 "\sub " Aci
#nr p"\if n end\if c end!" e"!" Aci
#cc e4 p'\var "' P'"\ins #usc' Aci    Kat.Nr. uebergeben an FLEX
  #cc e4 =sc      Kat.Nr. uebergeben an FLEX
#cc p"\var ''\ins #usn!" e"!" Aci
#nra b4 p'\var "' P'"\ins #usn' Aci
#cc p"\exec rueck!" e"!" Aci

 #cc b3 e1 =yY          1.Zeichen zur Differenzierung bei gleicher Sign.
 #cc e$ y0 b4 AyY
#t{ s4 }
  statt dieser Zeile
#cc B$s e$ y0 =yY
#uyY y0 p"RÅckgabe " =Y~

  Anzeige des Nutzers, auf Wunsch aktivieren
#uAU +#cc e0
#nr +#uci e0
#cc $u " (" P")"
#uci =Z~
#uci dci e0
#t{C "\ql " }
#)c
#90Å
#cc e$ { "\qr Signatur: " }
#uAU +#90î e0
#cc $u p" (ausgeliehen)!" e"!"

#90î
#t{C "\ql " }

#)c


#(V
#9DH $u p72 P104
#9DH $u =Y~
#9DH $u "x f1nd |: B" P"\sho rec\dis" =Z~

#)V

#(w
#97w p{ C t20 "    " t21 t72 "Externe Datei" } P{ t104 t22 } e0
#97w "Externe Datei!" e"!" =Y~
#97w p"X z-exdat " =Z~
#97w P22 e0
#)w


  UP f. Exemplare
#(X
  #+-    falls nicht gewuenscht
#nr drA dvF =nr e0
#nra =nr e0
     #uvF = Verfuegbarkeits-Flag
#00 +#J1 P":E" |:2    erstes laden
#nr +#J5 e0    es gibt keins
#J1
#t{ t20 "Exemplare: " t21 }
#nr +#9DG e0
#00 P":E" |102
#-:

#9DG $e P". "
#9DG $s e" =" p"[" P"]"
#9DG +#9DH i4,l y0 " \i Ausgeliehen bis \i0 !" e"!"
#9DG +#J4 i4,W " momentan reserviert !" e"!"
#9DG +#J4 i4,g " im GeschÑftsgang !" e"!"
#9DG +#J2 i4,V " Vorgemerkt !" e"!"
#9DG +#J2 i4,v " Verlust !" e"!"
#9DG +#J2 $v c"p" " PrÑsenz !" e"!"
  es gibt verfuegb. Ex., Flag setzen
#dt +#J2 =vF e0
#9DH $R b6 e2 P"-" 
#9DH $R b4 e2 P"-" 
#9DH $R e4 
#J4 
#nra =rA

#J2
#t23
#J3
#<
#00 +: P":E" |103
#00 +#J6 e" = " p"t " |:8
#)X
#J6
#00 p{ t4 "Es gibt" t72 "Bestellungen" t104 } e0
#00 "Bestellungen!" e"!" =Y~
#00 e" = " p'x var "|: t ' P'?"\find\sho list' =Z~
#t4
  Vormerk-Flip nur, wenn kein Ex verfuegbar 
#uvF +#J5 e0

#nr p22 p"Noch nicht verfÅgbar, !" e"!" 
#t{ t72 "Vormerken" t104 } 
#unr y0 p123 P{ 160 125 }
#unr "Vormerken!" e"!" =Y~
#00 p"X a-ovorm " =Z~
#J5
#unr dnr e0
#)X

#(Y
#9GG p{ "Geb¸hrensatz" } e0 
#3n i4,s I5,p I4,s { "Schlagwort-Stammsatz" } e0
#t4
#t{ t72 "DETAILS" t104 }
#nr p"X cfga!" e"!" =Z~
#nr p"DETAILS!" e"!" =Y~

#)Y

   >>>>>>>>> Ende der Unterprogramme >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

#-}
  Hier evtl. etwas zum Abschluss des Ganzen
#+#

