  d-krtf.apr : a99-Anzeige incl. verkn. Satzstrukturen, tabellarisch
  2002-02-15 / 2014-01-20   mit ZAboM und ALFA
  Plus Nachladungen von ueber- und untergeordneten Saetzen
  Wenn keine hierarchischen oder verknuepften Saetze vorkommen,
  dann genuegt einfacheres Modell:  d-srtf.apr
  Pendant fuer HTML s. d-khtm.apr
  2014-01: "kritische Zeichen" eliminiert, auch in d-k.apt

pn="Hierarchie-Export RTF" 

  Fuellen Sie die Bereiche zwischen den mit <<<<<<<<<  und >>>>>>>>>
  markierten Zeilen mit Ihren Exportbefehlen aus!
  Meistens reicht es, den Abschnitt unter Sprungmarke #-Z anzupassen!
  (siehe ganz unten)

  Der aktuelle (anzuzeigende oder auszugebende) Satz wird PRIMAERSATZ genannt.
  Dabei handelt es sich um entweder

  A.  einen Hauptsatz
      wenn er unabhaengig gespeicherte Untersaetze hat, muss die
      Kategorie #00 mit  einer eindeutigen IdNr X besetzt sein.
      In der Anzeige sollen unter dem auptsatz alle untergeordneten
      Saetze erscheinen.
      Die untergeordneten Saetze haben dann in #00 IdNummern der Form  X+n
      mit einer Bandnummer n :

  B.  einen Untersatz
      dann enthaelt #00 die IdNr des Hauptsatzes PLUS eine Bandnummer
      z.B.  #00 123456+005=Vol.5
      (hinter '='  kann eine Bandnummer in Textform folgen, sonst wird
      die auf '+' folgende Nummer genommen, fuehrende Nullen werden beseitigt)
      In der Anzeige sollen stufenfoermig die unmittelbar uebergeordneten,
      aber nicht die nebengeordneten Saetze erscheinen.

  C.  beides, auch "Mittelsatz" genannt: der Primaersatz ist einerseits
      einem anderen untergeordnet, besitzt aber auch selbst Untersaetze.
      In der Anzeige soll fuer die untergeordneten Saetze nach A, fuer die
      uebergeordneten nach B verfahren werden.


  Es kann bis zu 5 untergeordnete Stufen geben (theoretisch noch mehr).
  Jede weitere Stufe enthaelt in der #00 oder #09 ein weiteres '+' .
  Dieses wird fuer den Nachladevorgang ausgewertet.

  *********** Grundparameter, nicht ohne Grund aendern: ***************
td-rtff   RTF-Steuerbefehle
tad-aw    ASCII->ANSI Codetabelle

zl=0      Zeilenlaenge, nicht besonders wichtig!
           denn es wird kein Zeilenvorschub gemacht:
ze=" "
zb=" -"   Umbruch bei Leerz. und Bindestrich
zm=0      keine Seitenschaltung
dx=1      Umcodierung einschalten

  ae="</table>"   fuer HTML aktivieren

i4=1    V14-Ersetzungen ausfuehren

  <<<<<<<<<<<<    Raum fuer Ihre Zwischenteile (Textkonstanten)
  Empfehlung: Nummern 30 bis 59
  >>>>>>>>>>>>    Ende der anwendungsspezifischen Textkonstanten
4="\par "

  Tabellen-Parameter RTF              erste Spalte:
18="\pard\uc1\tx2272\li1800\fi-1800\f4 "       breit
19="\pard\uc1\tx2272\li1800\fi-1800\f4 "         schmal
   \f6 = Schrift 6 = Verdana.  \f0 einstellen, wenn Sonderzeichen noetig

  18=""   fuer HTML
  19="<table>"   fuer HTML

13="\par "
20="\par \i "     Neue Tabellenzeile
21="\i0 \tab "      zwischen zwei Tab.Spalten
22=""            Ende der Tab.Zeile (bei RTF nichts)
23=t20 t21

29=""
28=""


p .20 "\par \tab "      Absatzende
p .170 1     Nichtsortierzeichen ausblenden
      RTF-Steuerzeichen maskieren:
p { "\{"
p } "\}"
p \ "\\"

  Hier gehts los

#t{ "\f6\fs24 " }

#nr dY~ dZ~ e0

td-k     d-k.apt laden  (das ist der Hauptteil, gilt f. HTML und RTF)
#+#

   <<<<<<<<< Unterprogramme hier anfuegen, wenn man welche hat <<<<<<<

   Abschnitt fuer die Intern-Anzeige mit Feldnummern (F5 in a99)
#-(

#t18
#nr =aF e0

#t29
#t{ t4 }
#nr p"x" e2 =dm

#t{ s0 }       oder 5 statt 4 wenn 3stelliges Schema!

  Das erste Feld nehmen und ausgeben
#u01

  Schleife f. alle weiteren Felder
#-)
  Das naechste, wenn noch eins da ist!
#u02 +) p4

               oder
 #L            alternativ: Anzeige mit Labels aus der CFG
#t{ t4 }
#t28
#+#


  Unterprogramme, meist Flips oder Links
  ZAboM
#(A
#00 p{ " ... " t72 "Titeldaten bearb." t104 } e0
#00 "Titeldaten bearb.!" e"!" =Y~
#00 "x f1nd ppn " P"\dis\sho rec\form Zeit" =Z~
#)A

#(B
#9DG $Y { t20 "Jg./Bde." t21 t72 } P{ t104 t22 }
#9DG +#00 $Y =Y~
#)B
#00 e0
#</
#00 p"x var '|: " P"-al?'\f1nd\dis\sho rec" =Z~
#)B

  Titeldaten zum Bandsatz anzeigen
#(C
#t{ t72 t66 "Gesamtdaten anzeigen" t98 t104 }
#nr "Gesamtdaten anzeigen!" e"!" =Y~
#9DA $T "x f1nd ppn " P"\dis\sho rec" =Z~
#)C

#(D
#9DG $Y { t20 "Jg/Band:" t21 t72 t66 } P{ t98 t104 }
#9DG $Y =Y~
#00 p"x f1nd ppn " P"\dis\sho rec\exec o-func" =Z~

#9DG $Y B"/" { " ... " t72 t66 "Neues Heft (zu Bd." } P{ ")" t98 t104 }
#9DG $Y B"/" p "Neues Heft (zu Bd." P")" =Y~
#00 p"x f1nd ppn " P"\dis\sho rec\exec z-heft" =Z~
 #9DA $J "x f1nd ppn " P"\dis\sho rec\exec z-heft" =Z~

#t{ " ... " t72 t66 "Umlaufende Hefte" t98 t104 }
#nr p"Umlaufende Hefte!" e"!" =Y~
#9DA $T "x find |: u-" P"/?\if l1 mes Momentan keine;end\ord d13\sho list" =Z~
#)D

  Abosatz
#(E
#t{ t72 t66 "Gesamtdaten anzeigen" t98 t104 }
#nr "Gesamtdaten anzeigen!" e"!" =Y~
#9DA $T "x f1nd ppn " P"\dis\sho rec" =Z~
#)E

#(F
#9DB e$ c"^Al" p" (nicht mehr lfd.)!" e"!"
#00 p{ " ... " t72 "Abodaten bearb." t104 } e0
#00 "Abodaten bearb.!" e"!" =Y~
#00 "x f1nd ppn " P"\dis\sho rec\form Abo" =Z~
#00 p{ " ... " t72 "Neuer Band" t104 } e0
#00 "Neuer Band!" e"!" =Y~
#00 "x f1nd ppn " P"\dis\sho rec\exec z-nband" =Z~
#)F

#(G
#t{ t72 t66 "Gesamtdaten anzeigen" t98 t104 }
#nr "Gesamtdaten anzeigen!" e"!" =Y~
#9DA $T "x f1nd ppn " P"\dis\sho rec" =Z~
#)G

#(H
#9DA +# $$ c"V000" " (Umlauf beendet)!" e"!"      Umlauf schon beendet
#00 p{ " ... " t72 "bearbeiten" t104 } e0
#00 "bearbeiten!" e"!" =Y~
#00 "x f1nd ppn " P"\dis\sho rec\form Heftuml" =Z~
#00 p{ " ... " t72 "Heft weitergeben." t104 } e0
#00 "Heft weitergeben.!" e"!" =Y~
#00 "x f1nd ppn " P"\exec z-heft wg" =Z~
#)H

  ZAboM: Anzeige der zur Zs gehoerigen Abos und Bde
  Typische Eintr.: Abo: |:zzz0081-999909 -Al ab 2007. Lief.: BR
                    Bd: |:zzz0081-999909 -Bl 7992=2008
#(Z
#t{ "\par " }
#00 dzi =zi e0
#9DA $T dzi =zi e0
  jetzt ist #uzi die TitelId. Gibt es einen Eintrag?
#uzi +#ux: p"zz" |:8
#)Z
#-%
#t{ "\par " }
  die gefundene Reg.Zeile
#ubo dbo e0
#ux: b" -al" p"lfd.Abo " =bo
#ux: b" -ae" p"alt.Abo " =bo
#ux: b" -ab" p"abb.Abo " =bo
#ux: b" -bl" B"=" p"akt.Bd " =bo
#ux: b" -bv" B"=" p"abg.Bd " =bo
#ux: b" -bu" B"=" p"unv.Bd " =bo
#ux: b" -a" P{"\fs22 "} e0
#ux: b" -b" P{"\fs18  -- "} e0
#ubo p72 P104
#ubo =Y~
#ux: "x f1nd |: _" P"\dis\sho rec" =Z~

  naechste Zeile
#uzi p"zz" |:9
#ux: +% c"#uzi" e0
#)Z

   Unterprogramm fuer URL
#(U
#t{ t20 "URL: " t21 }
#cc +#8eZ i5,: e0
#cc e$ ,"_\_/_" p'x var "' P'"\ins #ucc\exec winstart.flx' =Z~
 #cc e$ p"~start " =Z~
#cc B"\31?" =Y~
#cc B"\31?" p72 P104
#)U
#8eZ
#cc e$ p"X _access!" e"!" =Z~
#cc B"\31?" =Y~
#cc B"\31?" p72 P104
#)U

   ALFA Ausleihfunktionen, die anklickbaren Flips aufbereiten
#(c
#uAL +& e0   ALFA? dann #-È
#cc B"\31L" ,"_\31s_:_" e$ e" = " p{ t20 "Signatur: " t21 } P22

#uAU +#cc e0   Ausleihangabe nur zeigen, wenn #uAU gesetzt
#+-

#cc $u p{ t20 "ausgeliehen an: " t21 } P22
#)c

#-&
#t{ s0 }    um die Kat.Nummer auswerten zu koennen
#cc +#90% $u e0    teilfeld u vorh. -> Rueckgabe
#cc +#90Å $p e0    keine Ausl., wenn $p

   Zur Differenzierung falls gleiche Signaturen
 #cc b3 e1 { C "\qr " t72 "Ausleihe " }
 #cc e$ b4 P104
   statt dieser Zeile:
#uAU +#90@ e0
#cc +#uci ,"_\31L__" ,"_\31s_:_" B"\31s" e$ e" = " b4 p"Signatur: " p{ C "\qr " }
#90@
#cc ,"_\31L__" ,"_\31s_:_" B"\31s" e$ e" = " b4 { C t20 "Signatur:   " t21 t72 "Ausleihe " } P{ t104 t22 }
  #cc p'x var "|;Ausl. " P"an ?=" #uuS\ask\if "" end\yesno Ausleihen?\if n end\if c end\ins #uuS\var #uuS(e"=")\ins $u-!' e"!" =ci

  Die FLEX-Befehlsfolge f. Ausleihe zusammenbauen
#nr p"x var ''\ins #usn\!" e"!" =ci
#01 "sub " P"\" Aci
#02 "sub " P"\" Aci
#03 "sub " P"\" Aci
#04 "sub " P"\" Aci
#nra b4 p'var "' P'"\ins #usn\' Aci
#cc e4 p'var "' P'"\ins #usi\exec ausl' Aci
 #cc b3 e1 =yY          1.Zeichen zur Differenzierung bei gleicher Sign.
 #cc e$ y0 b4 AyY
#cc ,"_\31L__" ,"_\31s_:_" B"\31s" e$ y0 e" = " b4 =yY
#uyY y0 b4 p"Ausleihe " =Y~
#t{ s4 }
  Befehlsfolge im naechsten  #uZx  speichern
#uci =Z~
  und Zw.speicher #uci loeschen
#uci dci e0
  Feldanfangspunkt wieder auf Pos. 4
#t{ s4 }
  Rechtsbuendig abschalten
#t{C "\ql " }
#)c

  Rueckgabe
#90% e0
#uAU +#cc e0   Rueckgabeflip nur setzen, wenn #uAU gesetzt
#nr +#90Å e0
   Zur Differenzierung falls gleiche Signaturen
 #cc b3 e1 { C "\qr " t72 "Ausleihe " }
 #cc e$ b4 P104
   statt dieser Zeile:
#cc ,"_\31L__" ,"_\31s_:_" B"\31s" e$ e" = " b4 { C t20 "Signatur:  " t21 t72 "R¸ckgabe " } P{ t104 t22 }

#nr p"x yes RÅckgabe?\if not yes end!" e"!" y1 =ci
#01 "\sub " Aci
#02 "\sub " Aci
#03 "\sub " Aci
#04 "\sub " Aci
#nr p"\if n end\if c end!" e"!" Aci
#cc e4 p'\var "' P'"\ins #usc' Aci    Kat.Nr. uebergeben an FLEX
  #cc e4 =sc      Kat.Nr. uebergeben an FLEX
#cc p"\var ''\ins #usn!" e"!" Aci
#nra b4 p'\var "' P'"\ins #usn' Aci
#cc p"\exec rueck!" e"!" Aci

 #cc b3 e1 =yY          1.Zeichen zur Differenzierung bei gleicher Sign.
 #cc e$ y0 b4 AyY
  statt dieser Zeile
#cc e$ y0 b4 =yY
#uyY y0 e" = " b4 p"RÅckgabe " =Y~

#t{ s4 }
  Anzeige des Nutzers, auf Wunsch aktivieren
#uAU +#cc e0
#nr +#uci e0
#cc $u " (" P")"
#uci =Z~
#uci dci e0
#t{C "\ql " }
#)c
#90Å
#cc e$ b4 { "\qr Signatur: " }
#t{ s4 }
#uaU +#90î e0
#cc $u p" (ausgeliehen)!" e"!"

#90î
#t{C "\ql " }

#)c

#(w
#97w p{ C t20 "    " t21 t72 "Externe Datei" } P{ t104 t22 } e0
#97w "Externe Datei!" e"!" =Y~
#97w p"X z-exdat " =Z~
#t22
#)w


  UP f. Exemplare
#(X
  #+-    falls nicht gewuenscht
#nr drA dvF =nr e0
#nra =nr e0
     #uvF = Verfuegbarkeits-Flag
#00 +#J1 P":" |:2    erstes laden
#+-    es gibt keins
#J1
#t{ t20 "Exemplare: " t21 }
#nr +#9DG e0
#00 P":E" |102
#-:
#9DG +#J3 i4,V e0
#9DG $e P". "
#9DG $s e" ="
#9DG +#9DH i4,l y0 " \i ausgeliehen bis \i0 !" e"!"
#9DG +#J4 i4,W " momentan reserviert !" e"!"
#9DG +#J2 i4,v " Verlust !" e"!"
#9DG +#J2 $v c"p" " PrÑsenz !" e"!"
  es gibt verfuegb. Ex., Flag setzen
#dt +#J2 =vF e0
#9DH $R b6 e2 P"-" 
#9DH $R b4 e2 P"-" 
#9DH $R e4 
#J4 
#nra =rA

#J2
#t23
#J3
#<
#00 +: P":E" |103
#t4
  Vormerk-Flip nur, wenn kein Ex verfuegbar 
#uvF +#J5 e0
#nr p72 p"Vormerken!" e"!" 
#unr y0 p123 P{ 160 125 }
#unr "Vormerken" =Y~
#00 p"X a-ovorm " =Z~
#J5
#unr dnr e0
#)X

#(t
#t{ " --- " }
#(T

 #t{ '<a href="event:a30get.php?urN="' }
 #nra
 #t{ '">Titel:</a>' t66 }

#cc $a e":" |92
#20
#8n " (" P")"
#</
#t{ t98 t22 }
#cc $a b":" p{ t20 "Abo:" t21 t66 } P{ t98 t22 }
#udt ddt e0
#cc $d p{ t20 "R.Datum:" t21 t66 } =dt
#udt b6 e2 f"0"
#udt b4 e2 f"0" "."
#udt e4 "."
#t{ t98 t22 }
#udt ddt e0
#cc $D p{ t20 "Eingang:" t21 t66 } =dt
#udt b6 e2 f"0"
#udt b4 e2 f"0" "."
#udt e4 "."
#t{ t98 t22 }
#cc $Y p{ t20 "Band:" t21 t66 } P{ t98 t22 }
#cc $p =bT e0
#cc $t =bT e0
#cc $c p{ t20 "WÑhrung:" t21 t66 } P{ t98 t22 }
#cc +#99z $q e0
#cc $p p{ t20 "GesamtBetrag:" t21 t66 } P{ t98 t22 }
#99z e0
#cc $q p{ t20 "GesamtBetrag EUR:" t21 t66 } P{ t98 t22 }
#cc +#99z $u e0
#cc $t p{ t20 "TeilBetrag:" t21 t66 } P{ t98 t22 }
#99z e0
#cc $u p{ t20 "TeilBetrag EUR:" t21 t66 } P{ t98 t22 }
#cc $x p{ t20 "Extrakosten:" t21 t66 } P{ t98 t22 }
#ubT x"+bt" =bt
#)T
#)t

   >>>>>>>>> Ende der Unterprogramme >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
