  D-1.APR : PRESTO- Bildschirmanzeige mit der Mîglichkeit, SÑtze nachzuladen       V15
  970721  Ohne Ausleih-/Erwerbungsdaten
  000425 (Version MIT: si. DEMO2)
  Mit Ergaenzungen fuer "capriccio"
  1. a) BÑnde von mehrbd. Werken (hierarchisch #01...#05)
     b) VerknÅpfungen per #09 IdNr+xxx  (1stufig aufwÑrts, Pica)
     c) VerknÅpfungen per #00 IdNr+xxx+xxx+...  (mehrstufig)
  2. StammsÑtze von Zeitschriften fÅr Aufsatzdaten (#70)
         Åber KÅrzel, ISSN, oder CODEN (Index 5, Abschnitt _ )
  3. AufsÑtze aus SammelbÑnden (#84) Åber  Id.Nr.
  4. Serientitel (#85) ebenfalls Åber KÅrzel per Index 5
  5. StammsÑtze Sacherschliessung (#3n ...)
  6. MUSIK : #52 / #22 als Kopf der Einheitskarte
  7. KUNST : #35, #36 , #62   (Capriccio, T. Berger 951206)
  8. FLIP : einige Beispiele unter #-` (raus, wenn nicht gewuenscht)

  evtl. anpassen: Signatur-Unterprogramm #(S

zl=75       ZeilenlÑnge (nach Bedarf zu kÅrzen)
zi=0        keine EinrÅckung
fl=12       Anzahl Zeilen je Karte
fm=2        Modus: Einzelkarten fÅr die BÑnde (1 = keine Einzelkarten)
dx=1        =0, wenn Farbe nicht gewÅnscht

i4=1        V14 Ersetzungsmodus
i5=_
i6=10       V23: Reg.10 fuer die V23-Ersetzungen
  fb 13 10 10 10   Kartenvorschub : 3 Leerzeilen
                   (fÅr Kartendruck: Farbbefehle rausnehmen, s.u.)

ab=""
as=""


  ********* ZWISCHENTEILE  1 - 78  *************
1=". "      Interpunktion
2=", "
3="; "
4=C
5=" : "
6=C C 
7=" = "
8=". - "
9=" / "
10="("
11=")"
12=C C C
13=C "   "
14=". - ("
15="." C "   "

19=C &0    neue Zeile, links an den Rand

30=C &0 "Kongr.: "

60=#250     EinrÅckung auf momentaner Position festsetzen

   Farben:  (entfernen, wenn nicht anwendbar, z.B. Kartendruck oder UNIX  !)
71=27 91 49 59 51 49 109          rot
72=27 91 49 59 51 50 109          grÅn
73=27 91 49 59 51 51 109          gelb
74=27 91 49 59 51 52 109          blau
75=27 91 49 59 51 53 109          magenta
76=27 91 49 59 51 54 109    reset / hellblau (normal)
77=27 91 53 59 51 55 109          weiss   mit BLINK
78=27 91 49 59 51 55 109          weiss

    flag #utt ist "1", wÑhrend eine Nachladung bearb wird, sonst "2"
ak=zz+0
#-0
    Satztypen differenziert behandeln:
#3n +W e0         Sacherschliessungs-Stammsatz
#3t +T e0         Thesaurus-Satz
#4n +P e0         Personenstammsatz
#6n +N e0         Kîrp.Stammsatz
#2n +E e0         EST-Stammsatz
#8n +R e0         Serien-St.
#9DG +B e0        Bestandssatz
#9A. +A e0         Adressensatz
#9B +A e0         Benutzersatz
#9D. +A e0        Erwerbungssatz
#9G. +A e0        Gebuehrensatz
#00 +Q i4,q e0    FAQ-Satz
#96b +L e0        Begriff-/Erlaeuterungssatz   ~~b

#9sa +# p{ t71 "Verweisung: " t76 }

    ******  Hauptteil : Konstr. der "Einheitskarte" ****
    ** Durchlauf ab hier fÅr Hauptsatz und UntersÑtze **
#-@
#uAT +Z i4,1 e0    Alternative Anzeige

#01 +g e0      fÅr BÑnde eines normalen mehrbÑndigen Werkes
#02 +g e0      wird das Folgende nicht gemacht
#03 +g e0
#04 +g e0
#05 +g e0
#20 +a e0

  prÅfe: gibt es einen Åbergeordneten Satz?
      Wenn es Åbergeordnete Einheiten gibt, werden diese zuerst geladen:
      (in #00 kommt dann '+' vor, dieser Teil wird zuerst abgeschnitten,
      dann im Index 9 nach der restlichen Id.Nr. gesucht)
#-a
#00 +a c"+" t"+" |90      rekursiv! bis kein '+' mehr vorkommt
        Nun sind evtl. mehrere Åbergeordnete Stufen geladen
#09 +a t"+" |90     #09 enth. Åbergeordn. Nr. (Ö la MAB oder Pica)
#<+b    war wirklich was geladen worden?, dann #-b
#+k     nein, dann #-k

  jetzt arbeiten wir die öberordnungen ab:
#-b
#<^    zuerst wieder hochschalten (mit #<+b war 1 runtergeschaltet worden!)

       Der oberste, d.h. zuletzt geladene Satz (Hauptaufnahme) wird ausgegeben
#40 +#90 e" = " e";" F" " P": "   erste Zeile: 1.Verf. und Signatur

#60 +#90 e"[_=]" F" " E60 P": "   erste Zeile: 1.Verf. und Signatur

!20 +#90 e" : " U E40    oder Anfang des Sachtitels, evtl. f. Kartendr. aktivieren
!8n +#90 e" : " U E40    Serientitel, wenn #20 nicht besetzt
#90 +n >S e0
#t4    Leerzeile, wenn keine Sign.
#-n
#nr p"1" e1 =tt Z     besetze #utt als flag mit "1"

   ***** Programmteil fÅr Aufnahme-Korpus *******
#-H
#18 e" : "
#wh
#18 b" : " p5
#18 +#71 e0 #69 8 #72 1 #zz 8      wenn #18 besetzt, bei #71 weiter
#19 e" : "
#wh
#19 b" : " p5
#19 +#39 e0 #69 9 #zz 8    wenn #19 besetzt, bei #39 weiter

#21 p"[" P"]" P{ C }

#nr dhs dtz e0

#20 +#20 e" : " dtz ahs
#8n ahs
#20 b" : " atz

#uhs
#wh             bei Fortsetz.karten bis hier wiederholen
#utz p" : "
#24 p" = "
#uhs e0 #24 0 #25 5 #69 9 #zz 8
#-i
#25 0 #24 0 #69 9 #zz 8

  #24 ++ p7 m". " #25 0 #69 9 #zz 8

#39 +#71 0 #69 1 #72 8 #zz 1      wenn #39 besetzt, bei #71 weiter

#40 ++ >K m>k #40z 3 #69 3 #70 0 #76 8 #zz 0
#60 e"[_=]" #69 3 #76 8
#41 ++ >K m>k #412 3 #69 >l #zz >m
#42 >K #69 >l #zz >m        Die Unterprogramme machen folgendes:
#43 >K #69 >l #zz >m           #(K macht die Invertierung "Vorname Name"
#44 >K #69 >l #zz >m           #(l : "[Funktionsbez.]; "   hintersetzen
#45 >K #69 >l #zz >m           #(m : "[Funktionsbez.]. - "
#46 >K #69 >l #zz >m        wenn also ein weiterer Personen- oder
#47 >K #69 >l #zz >m           Kîrperschaftsname folgt, wird "; " als

#48 >K #69 >l #zz >m
#49 >K #69 >l #zz >m

#50 >K #69 >l #zz >m           Postfix ausgegeben, sonst ". - "
#51 >K #69 >l #zz >m
#52 >K #69 >l #zz >m
#53 >K #69 >l #zz >m
#54 >K #69 >l #zz >m
#55 >K #69 >l #zz >m

#56 >K #69 >l #zz >m
#57. ++ >K #00 >m #69 >l #zz >m
#58. ++ >K #00 >m #69 >l #zz >m

#74 Z   nur wg Postfix

#61 ++ e"" #62 1 #72 8 #zz 1
            unselbstÑndiges Werk?
#70 +#83 e0      wenn #70, dann #71 bis #77 Åbergehen
#70c +#83 e0     oder #70c
#70i +#83 e0     oder #70i

#71 0 #71 1 #72 2 #zz 1
#72 0 #zz 1

#73 +#76 13 P", "
#74 +#75 e"" 13 #70 0 #75 5 #76p 2 #95 8 #zz 0
#819 +#75 e":" 13 #70 0 #75 5 #76p 2 #95 8 #zz 0
#00 +#75 c"+" e0  nicht bei #00 ...+  Untersaetzen!
#09 +#75 Z        nicht bei #09 ...+  Untersaetzen!
#hi +#75 e0   nicht auf Stufe #01
#t{ C "   o.O. : " }
#75 0 #76p 2 #77 8 #zz 0
#76 0 #76p 2 #77 8 #zz 0
#76p p" [Ersch.:] "
#77 ++ p" " m". - "

#81F { C t78 "FrÅher:    " t76 }
#81S { C t78 "SpÑter:    " t76 }
#81Z { C t78 "Zeitweise: " t76 }
#81B { C t78 "Beigabe:   " t76 }

  % Strange: Indirektes MehrfachprÑfix funktioniert nicht.
#62 ++ p{C &0 "Ausst.: " #250} e"" P"." m{" ; "}

#62 p{ #0 } e0
#83 ++ p{t30 #250} P"." m{" ; "}
#83 p{ #0 } e0 

   ******** Quellenangabe bei AufsÑtzen ***********

#70 +#8n i4,_ e";" F" " P"  " |52    wenn #70 _kÅrzel : nachladen
#70 +à e";" C p"In: "   wenn  #70 text  vorkommt, ausgeben, weiter bei #702
#70c +#8n e5 p"C" P"  " |92        in #70c steht CODEN?
                       danach wird im Index 9 gesucht und der Satz geladen.
#70i +#8n e9 p"I" P"  " |92         wenn kein CODEN, dann ISSN?
#+h   nichts davon: weiter bei #-h

#8n +#70 C p"In: "    der geladene Ansetzungstitel aus #8n wird genommen
            hier #8n1... einsetzen, wenn Zitiertitel 1 u.a. gewÅnscht!
#20 C e" :" p"In: "     wenn #8n nicht besetzt, dann #20
#70 e0    nur als Sprungmarke
#<               zurÅckschalten, dann die Aufsatzdaten
#-à
#70 +h b";" f" " p" ; "      #70 titel ; heft...    ODER:
#702 p" [" P"]"    ELand  diese Angaben kînnen hier beliebig anders
#t{ " ; " }
#704               Band    zusammengesetzt werden
#76 B" = " p"(" P")"      Jahr
#706               Heft
#708 p{t8 "S. "}

#-h
#t88   kursiv ein

       alle Serientitel
#85. ++ >4 m>4

    wenn in #84 eine Stammsatznummer steht: nachladen und nach #-u
    (unselbstÑndige BeitrÑge aus SammelbÑnden) *******************
#84 +u i4,_ B"_" e";" F" " P87 |90
#09 +#84q e0     wenn #09, dann in #84 Textangaben zum Åbergeordn. Werk
#84 +q C e";" F" " p"In: " P87      nur wenn keine #09
#84q
#t87   kursiv aus
#81. ++ >N m>N
  #23. ++ { C t78 "Nebentitel: " t76 } m"; "

#23. =cc                     % Merker fuer Ausgabe der Wendung
#23. ++ >n m>n               - Nebentitel
#23F p{C t78} "FrÅherer Titel: |" e"|"                   - frÅherer Titel
#23F p76 ,"__ ; _"
#23P p{C t78 "Parallel ersch. Titel: " t76} ,"__ ; _"   - parallele Ausg.
#23S p{C t78} "SpÑterer Titel: |" e"|"                   - spÑterer Titel
#23S p76 ,"__ ; _"
#23Z p{C t78 "Zeitweiser Titel: " t76} ,"__ ; _"        - zeitweiser Titel
#818 { C t78 "Hochschulschrift: " t76 }

#87 C p"ISBN "
#88 C p"ISSN "
#52 +#24 e0     MUSIK
#22 p{ C t78 "Originaltitel: " t76 }
 Paralleltitel, entweder dieses:
 #24 ++ p{ C t78 "Paralleltitel: " t76 } m". "
 oder dieses:
#24 Z

#-f

#31 C ,"_;_;;_" u_; F";" ,"_;;_;_" { &0 t78 "Schlagwîrter: " #250 t76 }
#31g C ,"_;_;;_" u_; F";" ,"_;;_;_" { &0 t78 "Geogr. Schlagwort: " #250 t76 }
#31c C ,"_;_;;_" u_; F";" ,"_;;_;_" { &0 t78 "Kîrperschafts-SW : " #250 t76 }
#31k C ,"_;_;;_" u_; F";" ,"_;;_;_" { &0 t78 "Kîrperschafts-SW : " #250 t76 }
#31p C ,"_;_;;_" u_; F";" ,"_;;_;_" { &0 t78 "Personenschlagwort: " #250 t76 }
#31s C ,"_;_;;_" u_; F";" ,"_;;_;_" { &0 t78 "   Sach-Schlagwort: " #250 t76 }

#31t p{C &0 t78 "Titel-SW: " #250 t76}
#35 ++ p{C &0 t78 "Werk-SW : " #250 t76} m{C} P>X Z
#36 ++ p{C &0 t78 "KÅnstler: " #250 t76} m8 P>X Z
#t{#0}

#33B C e"_" { &0 t78 "Brit. Schlagwort: " #250 t76 }
#33L C e"_" { &0 t78 "LoC-Schlagwort : " #250 t76 }
#30b +#82 C { &0 t78 "Sachgruppen: " t76 } P" (bearbeitet)"
#30a C { &0 t78 "Sachgruppen: " t76 }

#82 C "NE: "     (NE-Vermerk)
#902 e"" p78 e0
#902 ++ p>T m>T
#90a ++ p>T m>T
#902 p76 e0


#92 ++ p>B m>b e0        Bestandszeilen
#90z { C t78 "Alte Signatur: " t76 }
#92 +#90X e0
#00 i4,z C p"wenn keine Bestandsangaben, evtl. im Zettelkatalog nachsehen!& " e"&" p78 P76
#>C   wenn keine #92 vorkommt

   Vielleicht gibt's noch unselbstÑndige Teile (enthalt./beigef. Werke)


#90X
#00 +p P"*" |92    wenn ja, dann den ersten nachladen
#+v   nein

   ****  Ausgabe der selbstÑndig gespeicherten Bestandteile in Kurzform:
    (der nÑchste Abschnitt wird zyklisch fÅr alle Untereinheiten durchlaufen)

#-r
#t4
#+s
#-p
#t{ C t78 "Hierin enthalten:" t76 C }
#-s
#t{ #0 &0 }
#40 e";" P": "
#20 E40 P"..."
#<
#00 +r P"*" |93     nÑchsten Band laden, dasselbe nochmal


#-v
#hi +#90 e0   wenn Untersatz, dann jetzt nach #90

   *** Hauptprozedur zu Ende, im  Normalfall Sprung  nach #-m ********

#utt +m i4,2 Z        bedingter Sprung, wenn "2" in #utt steht (normal)
#<                    wenn '1': Nachladung wird bearbeitet
#+c
#90 e0     nur als Sprungmarke von #-v aus!
 #90 >S e0          Sign. mit UP aufbereiten
 #90 p"Sign.: " R   Signatur des Bandes normal ausgeben
#+#


    **************************
    jetzt die Zwischenstufen abarbeiten, wenn was nachgeladen wurde

#-c
#<+d   geht's noch weiter runter?
#t{ C #0 &0 }

#+g    nein, wir sind schon auf der Ausgangsaufnahme

#-d
#<^    ja, dann erst wieder hochschalten, dann die
       Zwischendaten ausgeben
#t{ C }
#00 b"+" B"=" f" " P{ ". " #250 }
#19 +#40 0
#20
#40 e";" F" " p" / "
#41 e";" F" " p" / " P" [Hrsg.]"
#<+c        rekursiv zurÅckschalten

   **********************************************
   jetzt sind wir wieder auf der Ausgangsaufnahme
   hier kommen wir nur hin, wenn das ein Untersatz ist (#utt = 1)
#-g
    sind wir bei einem normalen Untersatz?
#00 +l c"+" b"+" B"=" f" " P". " P60   nein, ein selbstÑndig gespeicherter
#09 +l c"+" b"+" B"=" f" " P". " P60   nein, ein selbstÑndig gespeicherter
#84 +l T" ; " b"; " P". " P60   nein, ein selbstÑndig gespeicherter
#84 +l e0        (" ; " fehlt)
   wenn man "#01 1 =" hat, ist es ein enthaltenes Werk.
 #90 { &1 #5 } e0
  $$950509: C in den Zeilen #01 - #05
#01 +l { C &1 } B" = " c" =" p"EnthÑlt: !" e"!" p78 P{ #250 t76 }
#01 +l { C &1 } B" = " P{ ". " #250 }
#02 +l { C &4 } B" = " P{ ". " #250 }
#03 +l { C &8 } B" = " P{ ". " #250 }
#04 +l { C &11 } B" = " P{ ". " #250 }
#05 +l { C &14 } B" = " P{ ". " #250 }

#</     alle nachgeladenen beseitigen (falls welche da sind)
        (bei den mehrbÑndigen alter Art darf das nicht passieren, sonst
        wird zur Hauptaufnahme zurÅckgeschaltet, daher die obigen Befehle.)
#-k
#nr p"2" e1 =tt Z    besetzt flag #utt mit "2" = Normalfall
#52 +#22 e"[=;]" F" " P": "   MUSIK
#40 +#90 e"[=;]" F" " P": "   erste Zeile: 1.Verf. und Signatur
  bei kartendruck: nÑchste 2 Zeilen aktiv
#60 +#90 e"[_=;]" F" " P": "   erste Zeile: 1.Verf. und Signatur
!20 +#90 e" : " U E40   oder Titelanfang + Signatur   evtl. f. Kartendr.
!8n +#90 e" : " U E40   oder Serientitel
#22 +H e"" e"" P4    MUSIK
#90 >S e0
#t4
 #t4   aktiv, wenn weitere Leerzeile nach Signatur gewÅnscht (Karte)
#+H     zum Hauptteil

        ***** BandauffÅhrung formatieren ****************
#-l
#nr p"2" e1 =tt Z    besetzt #utt mit "2"
#90 >S e0
  wenn Bandverfasser unterstrichen: nÑchste zeile (Karte!)
 #40 e";" F" " P": " p97 P96   erste Zeile: 1.Verf. und Signatur, unterstr.
 sonst diese:
#40 e";" F" " P": "   erste Zeile: 1.Verf. und Signatur, unterstr.
#19 +i #32 0 #69 9 #77 8 #zz 0
#20 0 #00 1 #25 5 #32 0 #69 9 #77 8 #zz 0
#+i     zum Hauptteil


#-m
#01 +# e0      fÅr BÑnde eines normalen mehrbÑndigen Werkes
#02 +# e0      wird das folgende nicht gemacht,
#03 +# e0      sondern hier beendet
#04 +# e0
#05 +# e0

   ***** Es kînnten noch selbstÑndig gespeicherte BÑnde da sein: *******
         deshalb wird eine Nachladung Åber Index 9 versucht:

#00 +#99J e"=" F" " P"+" |92    wenn ja, dann den ersten nachladen
#+z   nein, keiner da

   ****  Ausgabe der selbstÑndig gespeicherten Untereinheiten in Kurzform:
    (der nÑchste Abschnitt wird zyklisch fÅr alle Untereinheiten durchlaufen)

#99J
#00 i4,z { C t78 "Beilagen:" t76 C } e0

#-j
#t{ N }    neue Karte fuer neuen Band
  #t4   keine neue Karte
#t{ #0 &0 }
#00 +#90 b"+" B"=" f" " C P". " P60   1. Fall: echte Hierarchie
#09 +#90 b"=" f32 C P". " P60             2. Fall: "=" Bandangabe in #09
#09 +#90 b"+" f"0" C P". " P60        3. Fall: "+" in #09
#84 T" ; " b"; " P"." P60                 4. Fall: textuelle nach-oben-Verkn.
#90 >S e0
#19 +#71 #zz 8
#20 0 #zz 8
#71 0 #zz 8
#76 B"=" #zz 8
#77.
#92 ++ p>B m>b e0        Bestandszeilen
#<
#00 +j e"=" F" " P"+" |93     nÑchsten Band laden, dasselbe nochmal
       keiner mehr zu finden
#-z    dann Schluss machen,
#</    alle nachgeladenen SÑtze beseitigen
#t{ C &0 t78 "[" t71 "a" t78 "] = Anzeige umschalten   " t76 }
#8n +#89S e0
#00 +#89S i4,z e0
#85 +#89S e0
#t{ C }
#+#

#89S
#t{ t78 "[" t71 "5" t78 "] = Serienregister" t76 }
#+#
    ****** ENDE **************

    ****** Sonderabschnitte **

#-u     unselbst. Werk: Stammsatz ist geholt, jetzt Titel+Verf. ausgeben
#20 C "In: "
#40 e";" F" " " / "
#41 e";" F" " " / Hrsg.: "
#<    zurÅckschalten

#-q              Sammelband-Beitrag
#84 b";" f" " p8   entweder #84 _nnn ; Seiten
#708 p8 p"S."      oder die Seiten stehen in #708
#<^
#90 C >S           noch die  Signatur aus der Hauptaufnahme nehmen
#<
#+f   SchlW

#-A     Adressensatz
#9A +#9AA i4,A e0
#+V

#9AA
#9A N
#9A n C
#9A a C
#9A t C "Tel. "
#9A O "ôffn: " C
#9A B " (" P")"
#9A Z C
#9A L C "Sigel: " R
#+#


#-E        Stammsatz fÅr EST
#t{ "              EST: " #250 }
#2n
#2nr { C C &0 "Verweisungsformen: " #250 }
#2nu { C C &0 "  Andere Sprachen: " #250 }
#23 { C C &0 "Verweisungsformen: " #250 }
#2nz { C C &0 "        Anmerkung: " }
#+#

#-L     Begriff-/Erlaeuterungssatz        (komplett neu)    ~~b
#96b p{ t78 "     Begriff: " t76 #250 }
#96e p{ C C &0 t78 " ErlÑuterung: " t76 #250 }
#+#

#-N      Stammsatz fÅr Kîrperschaftsnamen
#t { t78 "          Name: " t76 #250 }
#6n
#6nd { C &0 t78 " 'Lebensdaten': " t76 }
#6na { C C &0 t78 "   Verw.Formen: " t76 #250 }
#6nf { C C &0 t78 "  frÅher siehe: " t76 #250 }
#6ns { C C &0 t78 "  spÑter siehe: " t76 #250 }
#6nr { C C &0 t78 "    siehe auch: " t76 #250 }
#6ue { C C &0 t78 "    engl. Form: " t76 #250 }
#6nz { C C &0 t78 "     Anmerkung: " t76 }
#+#   das waer's

#-P    Stammsatz fuer Personennamen
#t { t78 "          Name: " t76 #250 }
#4n
#4nd { C &0 t78 "   Lebensdaten: " t76 }
#4na { C C &0 t78 "   Verw.Formen: " t76 #250 }
#4np { C &0 t78 "    Pseudonyme: " t76 #250 }
#4nt { C &0 t78 "   wirkl. Name: " t76 #250 }
#4nr { C &0 t78 "    siehe auch: " t76 #250 }
#4nz { C &0 t78 "     Anmerkung: " t76 }
#+#

#-R     Stammsatz fÅr Reihe/StÅcktitel-Serie
#8na p" "
#90 R
#t { C C t78 " Zeitschrift/Serie: " #250 t76 }
#60 P": "
#8n 0
#8n1 { C &0 t78 "     Zitiertitel 1: " #250 t76 }
#8n2 { C &0 t78 "     Zitiertitel 2: " #250 t76 }
#74 C e""
#75 ": "
#76p { C C &0 t78 "     Ersch.Verlauf: " t76 }

#8nr { C C &0 t78 " Verweisungsformen: " #250 t76 }
#88c { C &0 t78 "             CODEN: " t76 }
#88 { C &0 t78 "              ISSN: " t76 }
#8nz { C C &0 t78 "         Anmerkung: " t76 }
#t { #0 C }
#+f                u.a. Bestandsangaben

#-B    Bestandsdatensatz
#9DF "IdentNr. :  "
#t4
#9DF +#40 e"+" e"=" |90    Titelsatz holen
#9DF b1 e"+" e"=" |90    Titelsatz holen
#40 P{ " : " C }
#20 P4               Titel anzeigen
#60 e"" " / "
#61 e"" " / "
#</
#9DG L "Bestand von " P": "
#9DG l C "Standort     : "
#9DG B C "Band         : "
#9DG b "   [" "]"

#9DG H C "Bestand      : "
#9DG n C "Anmerkung    : "
#t4
#9DG e p"               " P". Exemplar  "
#9DG v c"p" "  PrÑsenz!" e"!"
#9DG v c"a" "  ausleihbar!" e"!"
#9DG v c"k" "  Kurzausleihe!" e"!"
#9DG E C "Ausgabe      : "
#9DG Y C "E.-Jahr      : "
#9DG d C "Zugangsdatum : "
#9DG z C f" " "Zugangsnummer: "
#9DG a C "Barcode      : "
#9DG s e4 C "Signatur     : "
#9DG +# s b4 e4 x">1000" p"-"
#9DG s b4
#+#
#-T    Stammsatz fÅr Thesaurus-Eintragungen
#t {"Thesaurus-Stammsatz" C C "  Hauptbegriff: " #250 }
#3t
#3ta { C C &13 "UF: " #250 }
#3tr { C C &13 "RT: " #250 }
#3tb { C C &13 "BT: " #250 }
#3tt { C C &13 "TT: " #250 }
#3tn { C C &13 "NT: " #250 }
#3s { C C &13 "CC: " #250 }
#3sd { C C &13 "DI: " #250 }
#+#

#-Q     FAQ-Satz
#20 p{ t78 "   Frage: " t76 #250 }
#81 p{ C C &0 t78 " Antwort: " t76 }
#+#

#-W           Sacherschliessungs-StammsÑtze
#3n +w i4,c b1 e" " { t78 "      Sachgruppe: " t73 #250 } #zz 76
#3n +w i4,l b1 e" " { t78 "         Sprache: " t73 #250 } #zz 76
#3n +w i4,g b1 e" " { t78 "   Geograph.Code: " t73 #250 } #zz 76
#3n +w i4,h b1 e" " { t78 "        Zeitcode: " t73 #250 } #zz 76
#3n +w i4,a e" " b2 { t78 "     Aufstellung: " t78 #250 } #zz 76
#3n +w i4,z b1 e" " { t78 "  Zeitschr.Klass: " t73 #250 } #zz 76
#3n +w i4,b b2 e" " { t78 "  BASIS-Klassif.: " t73 #250 } #zz 76
#3n +? i4,s i5,p b" " { t78 "     Personen-SW: " t73 #250 } F"#" #zz 76
#4n +? P76   kein Text in #3n aber #4n
#3n +? i4,s i5,t b" " { t78 "        Titel-SW: " t73 #250 } F"#" #zz 76
#3n +? i4,s i5,s b" " { t78 "         Sach-SW: " t73 #250 } F"#" #zz 76
#3n +? i4,s i5,k b" " { t78 "Kîrperschafts-SW: " t73 #250 } F"#" #zz 76
#3n +? i4,s i5,c b" " { t78 "Kîrperschafts-SW: " t73 #250 } F"#" #zz 76
#3n +? i4,s i5,g b" " { t78 "    Geograph.-SW: " t73 #250 } F"#" #zz 76
#3n i4,d b1 u^^ e" " { t78 "        DK-Zahl:  " t73 #250 } #zz 76
#3nt +y
#3n b" " p{ "   " #250 }
#3na u() C
#-y
#t{ C &0 #0 }
#+x
#-w
#t{ #1 }
#3nt +#3qd p" "
#3n b" " p" "
#-?
#3na f" " { C &0 t78 "         Synonym: " #250 t76 }
#>R
#-x
#3qd { C C &0 t78 "        Dewey 19: " #250 t76 }
#3ql { C C &0 t78 "   LC-Systematik: " #250 t76 }
#3qb { C C &0 t78 "   BASIS-Klassif: " #250 t76 }

  Beispiel fÅr #3n a... : Standort
#3n i4,a p{ C C &0 #0 "Eine Auswahl von BÅchern finden Sie" } e0
#3n +#3nr i4,a i5,1 p{ C t72 "im Lesesaal 1 (1. Obergescho·)" t76 } e0

#3nr e"" { C C &0 t78 "      siehe auch: " #250 t76 }
#3nr b"b" { C C &0 t78 "     Oberbegriff: " #250 t76 } e"f" e"s" e"n" e"a"
#3nr b"f" { C C &0 t78 "          frÅher: " #250 t76 } e"s" e"fn" e"f"
#3nr b"s" { C C &0 t78 "          spÑter: " #250 t76 } e"n" e"a"
#3nr b"n" { C C &0 t78 "   neuer Begriff: " #250 t76 } e"a"
#3nr b"a" { C C &0 t78 "   alter Begriff: " #250 t76 }

#3nz { C C &0 t78 "     Anmerkungen: " #250 t76 } e""
#3nz b"a" { C C &0 t78 "          Quelle: " #250 t76 } e""
#3nz b"b" { C C &0 t78 "      Definition: " #250 t76 } e""
#3nz b"c" { C C &0 t78 "     Bemerkungen: " #250 t76 } e""
#3nz b"S" { C C &0 t78 "    SWD-Sachgrp.: " #250 t76 } e""
#3nz b"P" "(Erfassung: " P")" R
#+#

#-V
#t{ t71 "Systemdatensatz" t76 }
#+#


#-Z  Alternative Anzeige   (nur als Beispiel, muesste ausgebaut werden)
#t{ #4 }
#40 "AU: " P19
#20 +#76 "TI: " P19
#8n "TI: " P19
#76 "PY: " P19
#77 "RD: " P19
#74 "PP: " P19
#75 "PU: " P19
#85 "SE: " P19
#87 "IS: " P19
#90 "CN: " P19
#00 "##: " P19
#t{ #0 }
#t{ C &0 t78 "[" t71 "a" t78 "] = Anzeige umschalten" t76 C }
#+#

#-`   FLIP-Verarb.
#u1 +#98Z c"a" e0
#u1 +#985 c"5" e0    5 wurde gedrueckt
#u1 +#983 c"3" e0    3 wurde gedrueckt
#u1 +#987 c"7" e0    7 wurde gedrueckt
#+#
#98Z
#uAT +#dt x"==1" p"0" e1 =AT   #uAT auf 0 setzen, wenn 1
#uAT +#dt p"1" e1 =AT         und umgekehrt
#nr p"1" =AT e0     auf 1 wenn noch unbesetzt
#dt p"|" e1
#+#
#985
#85 +# e";" F" " u "?|5" 
#8n +# u "?|5" 
#20 +# u "?|5" 
#+#
#983
#31 +# e";" F" " p"?|3" 
#31s +# e";" F" " p"?|3" 
#+#
#987
#30b +# e" " F" " p"?|7" P","
#30a +# e" " F" " p"?|7" P","
#+#


  ****** UNTERPROGRAMME ****************

#(R    #3n-Satz geladen
#3ng +É e";" p"G " P"    *" |72             Geosatz laden
#-$
#</
#3n +% i4,s I6,  b2 e" " e"˙" e"-" P"    *" |78      Sachgebiets-Stammsatz laden
#)R    kein Erfolg
#-É
#3nt +$ { C C &0 t78 "          Region: " #250 t76 }
#3n +$ b" " { C C &0 t78 "          Region: " #250 t76 }

#)R
#-%
#ux7 b"*" { C C &0 t78 "      Sachgebiet: " #250 t76 }
#)R

#(K         UP fÅr Personennamen
                         Verweisungsformen abschneiden, hinter ", " beginnen
#cc u e" = " e" <" b", " e";" e"u.a." F32 P" "
                         Verweisungsformen abschneiden, hinter ", " beginnen
                         " " dahintersetzen (es folgt der Nachname)
                         (aber nur, wenn das ", " nicht in OH)
#cc e" = " e" <" e", " e" ["
                         Text vor dem ", " nehmen
#cc e" = " b" <" e" [" p" <"
                         Ordnungshilfe Unmodifiziert
#cc b"u.a" p" u.a"
#cc b";" f" " p"; "    wenn mehrere Personen in einer Kategorie
#)K                      in beiden FÑllen kein Postfix! (#zz 0)

#(k            hier kînnte man den 2. und 3. Verf. anders behandeln!
#cc u e" = " e" <" b", " e";" F32 P" "   wie Sie hier sehen, wird dasselbe gemacht
#cc e" = " e" <" e", "                   wie bei #(K
#cc b" <" e" [" p" <"
#cc b"u.a" p" u.a"
#cc b";" f" " p"; "
#)k

#(l    zuerst wird ein weiteres Unterprogramm aufgerufen:
#>F    gib die Funktionsbezeichnung aus
#t3    schreibe "; "
#)l

#(m    dasselbe wie #(l, bis auf das Postfix:
#>F
#t8    schreibe ". - "
#)m

#(F     Funktionsbez. Ausgabe
#i1,5 +5   erste Kategorieziffer =5: weiter bei #-5
        4er-Kategorien:
#cc i2,1 p" [Hrsg.]!" e"!"
#cc i2,2 p" [Mitarb.]!" e"!"
#cc i2,3 p" [Bearb.]!" e"!"
#cc i2,4 p" [Begr.]!" e"!"
#cc i2,5 p" [Samml.]!" e"!"
#cc i2,6 p" [Texter]!" e"!"
#cc i2,7 p" [öbers.]!" e"!"

#cc i2,8 p" [mutma·l.]!" e"!"
#cc i2,9 p" [Beitr.]!" e"!"

#)F        Ende Unterprogramm F
#-5           #5er Kategorien
#cc i2,0 p" [Illustr.]!" e"!"
#cc i2,1 p" [Komment.]!" e"!"
#cc i2,2 p" [Komponist]!" e"!"
#cc i2,3 p" [PrÑses]!" e"!"
#cc i2,4 p" [Resp.]!" e"!"
#cc i2,5 p" [Vorw.]!" e"!"

#cc i2,6 p" [Nachw.]!" e"!"   #56

  #57:
#cc i2,7 i3,d p" [Dirigent]!" e"!"
#cc i2,7 i3,i p" [Interpret]!" e"!"
 #cc i2,8 b"[[]" p"["
#)F       Ende Unterprogramm F (!)


#(4         Unterprog. fÅr Serientitel:
            mîgliche Eingabeform: #85 Serientitel =p Ordnungsworte ; BdNr
            oder                  #85 _kÅrzel ; BdNr
#cc +#8ny i4,_ e" " F";" P"  " |52   Stammsatz? holen, nach #8n (vor V14)
#cc +#8nx e" =p " e" ; " ,"_  _ / _" C e"" p"("     daraus wird gemacht:
#8ny
#t{ C "(" }
#60 P": "
#8n +#39h 0                 Stammsatztitel
#20        wenn #8n nicht besetzt
#39h
#<
#8nx
#cc B" =p " b" ; " e"" p' ; '    (#85 Serientitel ; BdNr)
#t{ ")" }
#)4

#(B    Bestandsangaben

#cc L C P":" p73 P76
#cc +#92X s p"  "
#cc C { t71 "Bestand: " t76 }
#92X
#(C
#00 +#39X P"'" |108
#)B
#)C
#39X
#t{ C t78 "InstitutsbestÑnde: " t76 }
#-X
#ux: +#92A y0 P"xx" b72 e0
#ux: +#92B e0
#92A   Bestandszeile zu lang, Bestandssatz laden
#ux: |100
#t{ C #0 &0 }
#ux: b"'" e'"' F"-" p"SYSA:" |118     Adressenzeile aus Reg.11
#ux; C b":" p75 P76
#9DG H C dx:

#9DG dxy s p" = " =xy   Signatur auch wirlich holen!

#</

#92B
#ux: =xy

#ux: b"'" e'"' F"-" p"SYSA:" |118
#ux; C b":" p75 P76
#uxy C b" "

#00 +X P"'" |109
#t{ C #0 &0 }
#)B
#)C

#(b    Bestandsangaben
#cc +- i3,a p"/"
#cc +- i3,j p"/"
#cc +- i3,l p" "
#cc +- i3,r p" "
#cc +- i3,s p" "
#cc +- i3,p p" [" P"]"
#cc +- i3,v p" "
#cc L C P":" p73 P76
#cc +#92Y s p"  "
#cc
#92Y
#)b


#(S       Signatur-Unterprogramm
#cc e"" p"Signatur: " R      rechtsbÅndig ausgeben
#cc u C "ausgeliehen an " R      wegnehmen, wenn nicht gewÅnscht
#t4
#)S

#(T       fÅr weitere Exemplare
#cc C e"" p"weiteres Exemplar: " R      rechtsbÅndig ausgeben
#cc u C "ausgeliehen an " R      wegnehmen, wenn nicht gewÅnscht
#)T

#(X       Aufbereitung #35, #36   $$
#cc +#39Y e"" F32 b0                                % Angabe ohne Teilfeld
#cc b"a" e"" e" = " b", " e";"                     % 1. Verfasser
#cc b"a" e"" f32 e", " p" "                        % wie bei #(K
#cc b"a" b"a" e"" e" = " b", " e";" p3            %; 2. Verfasser
#cc b"a" b"a" e"" f32 e", " p" "
#cc b"a" b"a" b"a" e"" e" = " b", " e";" p3      %; 3. Verfasser
#cc b"a" b"a" b"a" e"" f32 e", " p" "
#cc t p": "                                         %: Titel    
#cc v p9                                            %/ Vorlageform    
#cc e p8                                            %. - Ausgabe    
#cc +#39Z f p8                                      %. - Verlag : Ort  

                   % UnselbstÑndig
#cc g p" In: "                                      %In: GT
#cc d 2                                             %, Band
#cc j p10 P11                                       %(Jahr)
#cc m                                               %Heft
#cc n p", S. "                                      %, S. Seiten
#nr +#39Y Z

#39Z               % SelbstÑndig
#cc j p2                                            %, Jahr
#cc g p14                                           %. - (GT
#cc d 3                                             %; Zhlg.)
#cc g p11 Z                                         %)

#39Y
#cc u p" / "                                        % Unterschlagwort
#)X 

#(N   Fussnoten (#81)
#cc I3,F I3,B I3,S I3,Z I3,8 I3,9 C
#)N

#(n   Nebentitel (#23)
#cc +#98z I3,F I3,P I3,S I3,Z I3,z Z
#)n

#98z

#ucc +#98z dcc p{C t78 "Nebentitel: " t76} Z   %   ? Wendung ausgeben
#t3                                            %   : sonst "; "
#98z
#cc ,"__ ; _"
#)n

   ******** Sonderzeichen ***********

p  255    Absatz

p ™ 1
q ™ 1

 p ; "; "
p  47
q  47

q a/z A
q Ñ "AE"
q î "OE"
q Å "UE"
q é "AE"
q ô "OE"
q ö "UE"
q · "SS"

 q [ 1
 q ] 1
q _ 0
p _ 0

p  1    Verweisungsstrich
q  1

  nur fÅr Druck:
 p  .27 83 49    Tiefstellung
 p  .27 83 48    Hochstellung
 p  .27 84       Aufhebung

 q  .27 84
 q  .27 83 49
 q  .27 83 48

p .13 20
