  DISPREAD.FLX : Datensatz vom Anzeigefeld in Datei ausgeben und
  2005-06-20      wieder einlesen (Display Read) / Zusatz zu zc.flx
  2016-02-11
  ACHTUNG: Nur direkt im Anschluss an zc.flx zu starten

  X = letzte mit help eingelesene Datei (von zc.flx erstellt)
var X
if not "edit.adt" jump error

  Anzeigefeld enthaelt evtl. mehrere Saetze aus zc.flx
  Gesamten Anzeigefeld-Inhalt in Datei eee.adt speichern
file eee.adt
z=0
  Schreibdatei ee.adt oeffnen
open x ee.adt
if no jump eeno
  und ausgegebene Datei zum Lesen
open eee.adt

  Schleife: zeilenweise Daten einlesen und in ee.adt ausgeben
    Ende bei erster Leerzeile oder Dateiende (nur erster Satz wird genommen)
:el
var ")]"
get +iV
if can jump ende
var (f")]" F" ")
if "" jump ende

if "#" z+1
ins $u
wri $u n
jump el

:ende
close x
close


  Zaehler <2? Dann war es keine kategorisierte Anzeige
if z<2 end

  Hierarchische Untersaetze wegnehmen, dann Datei einlesen!
  (Untersaetze koennten verschoben worden sein, dann wuerde
  das Zuordnen nicht klappen)
if not hier jump les
:lop
first sub
  ersten Untersatz ansteuern
next sub
  keiner mehr da, Schleife zu Ende
if no jump les
erase sub
jump lop

:les
  Datei einlesen
read rec ee.adt
  Hilfsdateien loeschen
delete ee.adt
delete eee.adt

disp
sho rec
  Bearbeiteter Satz jetzt zu sehen, was soll damit passieren?
var "Satz speichern?" n "Nein: noch weiter daran arbeiten" n "Abbruch: Änderungen verwerfen"
yesno
if cancel undo;end
if no end
  yes, dann speichern
put
end

:error
   Fehlermeldung aktivieren, wenn gewuenscht
var "Daten im Anzeigefeld nicht geeignet zum Einlesen!" n "Zuerst mit  X zc  Datensätze downloaden"
mes
end

:eeno
var "Datei ee.adt kann nicht geschrieben werden"
mes

