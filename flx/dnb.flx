// dnb.flx : Daten von der DNB holen via "portal.dnb.de"
// 20120612/20120725/20140317: Basis HTML der Web-Anzeige
// 20150713: Umstellung auf Basis MARCXML, Noch ohne GND

// Zum Testen:
// a) Wenn gestartet mit  X dnb xyz, dann wird Datei xyz gelesen und direkt verarb.
// b) Um nach Aenderung dieser FLEX-Datei nochmal denselben Satz zu testen:
//    mit  X dnb $ starten. (wenn a99 vorher nicht neu gestartet!)


  Abschalten der automat. Umcodierungen UTF-8 oder ANSI ->ASCII beim "insert"
set c0
set U0
set e0

// Mit Argument 0 oder $ oder Dateiname gestartet zum Testen?

if "0" jump CONVERT
if "$" jump CONVERT
// denn der XML-Text steht dann noch in $, geht dann viel schneller
// XML anzeigen lassen:  x var $\sho IV

//  X dnb dateiname
//  (wenn man sich einen XML-Text  unter dateiname (z.B. via Portal) gespeichert hat)
if not "" jump datei

// Normaler Anfang (Start mit  X dnb  ohne Argument)

// Wenn man nicht das A-Schema verwendet, geht's nicht!
var K1
xco clower
if not "a" help fremd;end

jump einstieg
// Damit wird :SF uebersprungen; (steht hier oben, weil's oft gebraucht wird)
// Enorm WICHTIG: UP fuer Isolierung eines <subfield> aus einem <datafield>
// Nutzung z.B. mit  var "245a"\perf SF
// d.h. iV enthaelt z.B.  245a
:SF
ins #uSF

//  z.B. entsteht hier  var $ (x'd t="245"' x's c="a"')
var "$ (x'd t=" 34 #uSF(0,3) 34 "' x's c=" 34 #uSF(3,1) 34 "')"
// hiermit wird der so fabrizierte Befehl ausgefuehrt:
var
sho IV

// Inhalt des subfield ist nun in iV
return


// Nutzer zur Eingabe auffordern
:einstieg
z=0

ask ISBN oder Stichwörter (evtl. gnd dazu, falls Normsatz gesucht)=#uis
if cancel end
if "" end

  Eingabe mit "ask" ist ANSI, intern soll es ASCII sein
asci
ins _ _+_
ins _-__
ins #uis

  Umcodierung der Eingabe mit Hilfe der Indexparameter
  und zwar mit p oder q, je nachdem, bei welchem Kleinbuchstaben rauskommen
  
var "Ä"
asci
xco iq
  q ist nicht richtig, dann p:
if not "ae" var #uis;xco ip;jump b
var #uis

// Umcodieren in Kleinschrift
xco iq

  Umcodierte Eingabe fertig
:b
// Bindestriche eliminieren
ins _-__
ins #uis


var "Sende Suchauftrag an DNB"
sho IV

// Befehl zusammenbauen
var "Fhttps://portal.dnb.de/opac.htm?query=" #uis "&method=simpleSearch"
// und absenden, d.h. die Daten in Frankfurt abholen.
var
// Antwort jetzt in der iV, normalerweise Ergebnisliste

jump anfang

// Aufruf war mit Dateiname
:datei
ins $xyz
var "F" $xyz
// Datei xyz in iV einlesen:
var
if "" var "Datei " $xyz " nicht gefunden";mes;end

// Inhalt umcodieren per u-Tabelle

xcode u

// einige Zeichen aendern
perf umwa
jump CONVERT

// In der iV stehen die Daten vom portal bzw. aus der Datei
:anfang



// utf-8 -> ASCII

// Wenn Wortfolge %...% vorkommt, ist es eine Liste:
if %class="number"% jump mehrere

  Sonst ein Satz, aber in Portal-HTML-Form!
:anfang2

  Nummer isolieren
var (b"MARC21-XML" b"info/" e"/a")
ins $dnbnr

  und damit die MARCXML-Form anfordern
var n n "Rufe XML-Daten ab für " $zeile
sho +IV
var "Fhttp://d-nb.info/" $dnbnr "/about/marcxml"
var
ins $

if not %leader% jump fehlanzeige

var $ (b"http://www.loc.gov/M" e'"' e"<")

 // Es kommt kein solcher Link vor, dann ist das kein Datensatz
if "" jump fehlanzeige

var n n "Daten empfangen, Umwandlung"
sho +IV

// Neuen Datensatz anlegen, vorher Nr. des aktuellen sichern
var "#" i
ins $idnr

//  *** Satz verarbeiten ***
:CONVERT
new 0
var $	
ins _^M^J__
ins _^J__
ins $

// Inhalt des Fremdsatzes ist jetzt in $
var $

// Verbositaet minimieren: <datafield tag="...">...</datafield> wird zu <d t="...">...</d>
ins _<datafield tag_<d t_
ins _<subfield code_<s c_
ins _<controlfield tag_<c t_
ins _</datafield_</d_
ins _</subfield_</s_
ins _</controlfield_</c_
// Statt jedesmal nach "<datafield" zu suchen, etc., dann nur noch "<d", "<s" und "<c". 

  UTF-8 -> ASCII
xcode u

  Kombinationen Buchstabe+Diakritikum umsetzen (SWB und DNB)
   fuer die in OstWest definierten Zeichen
  Diaerese
ins _a½_„_
ins _o½_”_
ins _u½__
ins _A½__
ins _O½_™_
ins _U½_š_
ins _e½_‰_
ins _i½_‹_
ins _y½_˜_

  Akut
ins _aµ_ _
ins _eµ_‚_
ins _iµ_¡_
ins _oµ_¢_
ins _uµ_£_
ins _Eµ__
ins _yµ_­_  
ins _nµ_õ_  
ins _Nµ_ô_  

  Gravis
ins _a¶_…_
ins _e¶_Š_
ins _i¶__
ins _o¶_•_
ins _u¶_—_

  Circonflex
ins _a·_ƒ_
ins _e·_ˆ_
ins _i·_Œ_
ins _o·_“_
ins _u·_–_

  Tilde
ins _nè_¤_
ins _Nè_¥_

  Cedille
ins _cÓ_‡_
ins _CÓ_€_
ins _sÓ__
 ins _tÓ_ê_

  breve
ins _a¾_ä_
ins _g¾__

  Ringel [Statt dessen kommt ø=Grad vor]
ins _aÏ_†_
ins _AÏ__
ins _uÏ__
ins _uø__
ins _UÏ_Ÿ_

  Doppelakut
ins _oÆ__
ins _uÆ__

  Ogonek
ins _aÒ_î_
ins _eÒ_ï_
ins _uÒ__

  hacek
ins _eÇ__
ins _cÇ_§_
ins _CÇ_¦_
ins _rÇ_¨_
ins _RÇ_©_
ins _sÇ_¬_
ins _SÇ_«_
ins _zÇ_¯_
ins _ZÇ_®_

  Nichtsort.z.
ins _&#152;_ª_
ins _&#156;_ª_
ins _&lt;_<_
ins _&gt;_>_
ins _&amp;_&_

  >> <<   (fehlt in ucodes.apt)
ins _Â»_"_
ins _Â«_"_

  restliche separaten Akzente von rechts nach links
dow Ai
  das ist nun der Ausgangstext fuer alles
ins $

  HAUPTTEIL : Die eigentliche Umwandlung

var "024a"
perf SF
if %info/gnd% jump gnd

var (T"/")
ins #00
ins #89D

// DNB-Nr. in #00 - wg. Verkn. bei mehrteiligen Sachen
var $ (x'c t="001"')
ins $a
var "dnb" $a
ins #00

    Unterprog. :SF (s.o.) holt einen Unterfeldinhalt, z.B. 020$a:
var "020a"
perf SF 
hyp
ins #87

var "022a"
perf SF 
ins #88

  Regelwerk
var "041e"
perf SF
if not "" ins #10
var "040e"
perf SF
if not "" ins #10

var "773w"
perf SF
var (b")")
if "" jump 100
ins $w
var "773q"
perf SF
ins $q
var $w "+" $q
ins #09
var ""
ins $w
ins $q


:100
var "100a"
perf SF 
ins $a

var "100c"
perf SF 
ins $c

var "100d"
perf SF 
ins $d

var $a
if $c var +", " $c
if $d var +" ª<" $d ">"
ins #40
var ""
ins $a
ins $c
ins $d

  Inhaltstyp
var "336b"
perf SF
ins _^J^J_ _
ins #0c.1

// Medientyp (337) ist redundant, wird nicht uebernommen

  Datentraegertyp
var "338b"
perf SF
ins #77.1

  Sprachen
var "041a"
perf SF
ins _^T^T_, _ 
ins _^J^J_, _ 
ins #37

var "041h"
perf SF
ins _^T^T_, _ 
ins _^J^J_, _ 
ins #37o


var "110a"
perf SF 
ins $a
var $a
ins #60

  Konferenz
var "111a"
perf SF
ins $k
if not $k jump 730a

var "111n"
perf SF
var (F".")
ins $n
var "111d"
perf SF
ins $d
var "111c"
perf SF
ins $c

var $k " ; " $n " (" $c ")" " : " $d 

ins #83

:730a
var "730a"
perf SF
ins $a
var "730g"
perf SF
ins $g
var $a
if $g var +" <" $g ">"
ins #22
var ""
ins $a
ins $g

var "245a"
perf SF
ins $a

var "245b"
perf SF
ins $b

var "245c"
perf SF
ins $c

var "245n"
perf SF
ins $n

var "245p"
perf SF
ins $p

  sonstige unterscheidende Eig. v. Werk od. Expr.
var "381a"
perf SF
ins $s

var $a " : " $b
var (F" :")
if $n var +", " $n
if $p var +" " $p
if $s var +" [" $s "]"
ins #20

var "240a"
perf SF

ins #22

var "245b"
perf SF
if not "=" jump 770a
var (f"= ")
ins #24

:770a
  Beilagen, noch zu vervollst.
var "770a"
perf SF
ins #26


var $c
ins #39

var ""
ins $a
ins $b
ins $c
ins $n
ins $p
ins $s
ins $x
ins $y

  Art des Inhalts (z.B. Konferenzschrift Ort, Jahr)
var $

var (x'd t="655"')
ins _^J^J_^T^T_
ins $Au

var $Au (e"^T" x's c="a"')
ins $a
var $Au (e"^T" x's c="x"')
ins $x
var $Au (e"^T" x's c="y"')
ins $y

var $a " " $x ", " $y
var (F", ")
ins #31f
var ""
ins $a
ins $x
ins $y

var $Au (b"^T^T")
ins $Au
if not $Au jump 700
var $Au (e"^T" x's c="a"')
ins $a
var $Au (e"^T" x's c="x"')
ins $x
var $Au (e"^T" x's c="y"')
ins $y

var #31f " ; " $a " " $x ", " $y 
var (F", ")
ins #31f
var ""
ins $a
ins $x
ins $y

var $Au (b"^T^T")
ins $Au
if not $Au jump 700
var $Au (e"^T" x's c="a"')
ins $a
var $Au (e"^T" x's c="x"')
ins $x
var $Au (e"^T" x's c="y"')
ins $y

var #31f " ; " $a " " $x ", " $y 
var (F", ")
ins #31f
var ""
ins $a
ins $x
ins $y


:700
  Beteiligte Personen

var $
var (x'd t="700"')
ins _^J^J_^T^T_
ins $V
ins $v


:v40

var $v (e"^T" x's c="a"')
ins $a
var $v (e"^T" x's c="c"' e"^T")
ins $c
var $v (e"^T" x's c="d"' e"^T")
ins $d

var $v (x's c="4"' e"^T")

if "aut" jump aut
if "edt" jump edt
if "cmp" jump cmp
if "ilu" jump ilu
if "ant" jump ant
if "trl" jump trl
if "hnr" jump hnr
if %cnd% jump cnd

if "ctb" var ""
ins $r

var $a 
if $r var +" ª[" $r "]"
ins #42~


:v40m
var ""
ins $a
ins $d
var $v (b"^T^T")
ins $v


if $v jump v40
jump 260a

:aut
var $a 
if $d var +" ª<" $d ">"
ins #40~
jump v40m

:cmp
var $a 
if $d var +" ª<" $d ">"
ins #52~
jump v40m

:cnd
var $a 
if $d var +" ª<" $d ">"
ins #57d
jump v40m

:hnr
var $a 
if $d var +" ª<" $d ">"
ins #59~
jump v40m

:trl
var $a 
if $d var +" ª<" $d ">"
ins #47
jump v40m

:edt
var $a 
if $d var +" ª<" $d ">"
ins #41~
jump v40m

:ilu
var $a 
if $d var +" ª<" $d ">"
ins #50~
jump v40m

:ant
var $a 
if $d var +" ª<" $d ">"
ins #44~
jump v40m

  Ort
:260a
var ""
ins $a
ins $d
ins $r

var "260a"
perf SF
ins _^T^T_ ; _
ins _^J^J_ ; _
ins #74

  Verlag
var "260b"
perf SF
ins _^T^T_ ; _
ins _^J^J_ ; _
ins #75

  Jahr
var "260c"
perf SF
ins #76

  Neu: 264 (noch unvollst.)
var "264a"
perf SF
ins _^T^T_ ; _
ins _^J^J_ ; _
ins $a

  Verlag
var "264b"
perf SF
ins _^T^T_ ; _
ins _^J^J_ ; _
ins $b

  Jahr
var "264c"
perf SF
ins $c

  Typ (indic 2) 1:Publ., 2:Distrib., 3:Manufac., 4:Copyright
var $ (b't="264" ind1=" " ind2="' e'"')

if "1" jump n264
ins $i

if $a var $a
if $b var +" : " $b
if $c var +", " $c
ins $a
var $i
if "2" ins #73.2
if "3" ins #73.3
if "4" ins #73.4

:n264
if $a if not #74 var $a;ins #74
if $b if not #75 var $b;ins #75
if $c if not #76 var $c;ins #76

var ""
ins $a
ins $b
ins $c


  Umfang
var "300a"
perf SF
ins #77

  Umfang
var "250a"
perf SF
ins #71

  DNB Id
var $ (x'c t="001"')
ins #89D


var "500a"
perf SF
ins _^J^J_; _
ins #81

var "502a"
perf SF
ins #818

var #818(b": " e",") ":" #818(T", ")
if not ":" ins #819


//  490 mehrfach -> #85
// Problem: $v kann fehlen

var $ (x'd t="490"')
ins $x

:490

var $x (b'c="a">' e'</s>')
ins $a
var $x (b'c="v">' e'</s>')
ins $v

var $a
if $v var +" ; " $v
ins #85~


// Noch eine Serie?
var $x (b'c="a">' b'c="a">')
if "" jump 71
ins $x
var '  <s c="a">' $x
ins $x
jump 490
var ""
ins $a
ins $v

:71
//  710 mehrfach -> #61

var $ (x'd t="710"')
ins $x

:710
var $x ( e'</s>' b'c="a">')
ins $a
var $x (b'c="a">' e'</s>')
if not %pbl% var $a;ins #61~

// noch eine 710?
var $x (b'c="a">')
ins $x
if $x jump 710

var "711a"
perf SF
ins $a
var "711n"
perf SF
ins $n
var "711c"
perf SF
ins $c
var "711d"
perf SF
ins $d

var $a
if $n var +" ; " $n
if $c var +" (" $c ")"
if $d var +" : " $d 
if $a ins #83
var ""
ins $a
ins $c
ins $d
ins $n


var "520a"
perf SF
ins #98

var ""
ins $x

var "082a"
perf SF
ins _^T^T_ ; _
var (e" ; B")
ins #32B

var "650a"
perf SF
ins _^T^T_; _
ins _^J^J_; _
ins #31s

var "651a"
perf SF
ins _^T^T_ ; _
ins _^J^J_ ; _
ins #331

var "652a"
perf SF
ins _^T^T_ ; _
ins _^J^J_ ; _
ins #332

var "610a"
perf SF
ins _^T^T_ ; _
ins _^J^J_ ; _
ins #31c

var "610t"
perf SF
ins _^T^T_ ; _
ins _^J^J_ ; _
ins #31t

var ""
ins $b

jump 655
 // in 653 steht Sammelsurium von Termen. ONIX?
var "653a"
perf SF


ins _^M^J_ ; _
ins _^J^J_ ; _


  VLB-Codes u.a. wegnehmen
var (u() f")")
ins _Electronic book text__
ins _Fachpublikum__
ins _Paperback / softback__
ins _2947: Taschenbuch__
ins _TN000__
ins _)Paper over boards__
ins _)Cloth__
ins _Sachbcher/__
ins _/_ ; _
ins _Hardcover, Softcover__
ins _BC: Paperback__
ins _Hardback__
ins _1520:__

ins _; ;_;_

ins $A

:33l
var $A (e"; ")
ins $a
if %SC% jump 33n
if %SU% jump 33n
if %SOC% jump 33n
if %JH% jump 33n

var $b "; " $a
ins $b

:33n
var $A (b"; ")
ins $A
if not "" jump 33l

var $b (f"; ")
ins #33

:655

 // GND-SW, provisorisch
if #330 jump url

var "689a"
perf SF
ins _^T^T_; _
ins _^J^J_; _
ins #33G

:url
  // Mehrere URLs, noch nicht definitiv

var $ (x'd t="856" ind1=" " ind2="0"' x's c="u"' e"&amp;" e"")
ins $u
var $ (x'd t="856" ind1=" "' x's c="x"' e"")
ins $z
if $z var $u 31 "t" $z
if not $z var $u
ins #8e

var $ (x'd t="856" ind1="4" ind2=" "' x's c="u"' e"&amp;" e"")
ins $u
var $ (x'd t="856" ind1="4" ind2=" "' x's c="x"' e"")
ins $z
if $z var $u 31 "t" $z
if not $z var $u
ins #8e1

var $ (x'd t="856" ind1="4" ind2="2"' x's c="u"' e"&amp;" e"")
ins $u
var $ (x'd t="856" ind1="4" ind2="2"' x's c="3"' e"")
ins $z
if $z var $u 31 "t" $z
if not $z var $u
ins #8e~


var $ (x'd t="856" ind1="4" ind2="2"' b"^T" x's c="u"' e"&amp;")
ins $u
var $ (x'd t="856" ind1="4" ind2="2"' b"^T" e"" x's c="3"')
ins $z
if $z var $u 31 "t" $z
if not $z var $u
ins #8e~

var ""
ins $u
ins $z


  *** Satz fertig, anzeigen ***

:Display
sho rec
disp
// gleich das geeignete Formular aufmachen

yes Bearbeiten und Speichern? (Abbruch = Vergessen)
if cancel jump forget
if no end

exec onforms
set e1
end

// ACHTUNG: Noch nicht bearb! Aus dem alten dnb.flx
// Es ist ein Normsatz aus GND
// (Umwandlung noch zu erweitern, hier nur Werk, Person und Koerp.)
:gnd
var (T"/")
ins $a
var "gnd" $a
ins #00
ins #89D

  Entitaetencode
var "079v"
perf SF
ins #0c

  Regelwerk
var "041e"
perf SF
if not "" ins #10
var "040e"
perf SF
if not "" ins #10

  SW-Satz
var "150a"
perf SF
if not "" jump sw

  Werksatz
var "100a"
perf SF
if not "" var +": "
ins $a

var "100t"
perf SF
ins $t
if ARtic ins $t

if $t var $a $t
ins #2n

  Person
var "100a"
perf SF
ins #4n

var "100d"
perf SF
ins #4nd


var ""
ins $t
ins $a

var "400a"
perf SF
if not "" var +": "
ins $a

var "400t"
perf SF
ins $t
if not $t jump person
if ARtic ins $t

if $t var $a $t
ins #2nr

var ""
ins $a
ins $t
jump 110


:person
var $ (x'd t="400"')  

ins _^T^T^T_;;_
ins _^T__


:4na
ins $A
var ""
ins $p
var $A (e";;" x's c="a"')
ins $a

var $A (e";;" x's c="9"')
if %4:pseu% $p p
if %U:% jump 4nx
if not $p jump 4nb
var #4np "; " $a
ins #4np
jump 4nx

:4nb
var #4na "; " $a 
ins #4na

:4nx
var $A (b";;")
if not "" jump 4na

var #4na (f"; ")
ins #4na
var #4np (f"; ")
ins #4np



:110
  Koerperschaft
var "110a"
perf SF
ins $a
var "110g"
perf SF
ins $g

if $a var $a ". " $g
ins #6n
if #6n jump vw

var "151a"
perf SF
ins $a

if $a var $a 
ins #6n

:vw
  Verweisungsformen

var $ (x'd t="410"')  
ins _^T^T^T_;;_
ins _^T__

ins $A

:6na
var ""
ins $gins $b

var $A (e";;" x's c="a"')
ins $a
var $A (e";;" x's c="b"')
ins $b
var $A (e";;" x's c="g"')
ins $g
var #6na "; " $a ". " $b $g
var (F". ")
ins #6na
var $A (b";;")
ins $A
if $A jump 6na
var #6na (f"; ")
ins #6na

:451
var $ (x'd t="451"')  
ins _^T^T^T_;;_
ins _^T__

ins $A
:6nr
var $A (e";;" x's c="a"')
ins $a

var #6nr "; " $a
ins #6nr 
var $A (b";;")
ins $A
if $A jump 6nr
var #6nr (f"; ")
ins #6nr


  frueherer/spaeterer Name

  var "$ (x'd t=" '"510"' 39 "')"  
  var
  Das geht einfacher:
var $ (x'd t="510"')  
ins _^T^T^T_;;_
ins _^T__

ins $A

:fs

var $A (e";;")
if %4:vorg% #uvg 1
if %4:affi% jump 4affi
if not #uvg #unf 1

var $A (e";;" x's c="a"')
ins $a
var $A (e";;" x's c="g"')
ins $g
var $a ". " $g
var (F". ")

if #uvg ins #6nf
if #unf ins #6ns
jump 6nx
:4affi
var $A (e";;" x's c="a"')
ins $a
var $A (e";;" x's c="b"')
ins $b
var $a ". " $b
ins #4nz

:6nx
var $A (b";;")
ins $A
#uvg
#unf
if $A jump fs

  Konferenz
var "111a"
perf SF
ins $k
if not $k jump nname

var "111n"
perf SF
var (F".")
ins $n
var "111d"
perf SF
ins $d
var "111c"
perf SF
ins $c

var $k " ; " $n " (" $c ")" " : " $d 
ins #6n
jump nname

:sw
ins $s
var "ss " $s
ins #3n

  Synonyme
var $ (x'd t="450"')  
ins _^T^T^T_;;_
ins _^T__

ins $A

var ""
ins $t

:sy

var $A (e";;")
ins $a
var $a (b'c="a">' e"<")
ins $s
var $t "; " $s
ins $t

var $A (b";;" e";;")
if "" jump so
ins $A
jump sy

:so
var $t (f"; ")
ins #3na

var $ (x'd t="550"')  
ins _^T^T^T_;;_
ins _^T__

ins $A
var ""
ins $t

:ob

var $A (e";;")
if %4:ob% #uvg 1
if %4:th% #uvg 1

var $A (e";;" x's c="a"')
ins $a

if #uvg var $t "; " $a;ins $t
jump 3nx

:3nx
var $A (b";;")
ins $A
#uvg
if $A jump ob

var $t (f"; ")
ins #3nr


:nname

jump Display

:fehlanzeige
// ist es eine Liste mit mehreren Titeln?

var $
if %class="number% jump mehrere

ins $Fehl
sho IV

var "Fehlanzeige mit " #uis "!" n "Neuer Versuch?"
yesno 

if yes jump einstieg
set e1
end

:mehrere
z+1
var (b'class="number')
perf umwa
xco u
ins _^T__
ins _<br>_ / _

ins $LIST

var $LIST (b'class="number' b'=0">' e"<")
ins _^M^J__
ins $ti
var $LIST (b'"amount">' b" von " e"<")
ins $anz
 
// var "Da gibt's " $anz " Stück, z.B. " n $ti
// mes
// Ende abschneiden bei </table>, Dann Links zur naechsten Seite etc, nicht mehr da!
// var $LIST (b"Details zu diesem Datensatz" e"/table")
var $LIST (b"Details zu diesem Datensatz")
ins $LIST
var $LIST

// in $LIST gibt es Elemente, die so anfangen:

//   /opac.htm?method=showFullRecord&currentResultId=...
//  z.B.
//   /opac.htm?method=showFullRecord&currentResultId=hofstadter+douglas+escher%26any&currentPosition=3

//  Davor muss man setzen:
//    https://portal.dnb.de

open x erg.lst
wri "Es gibt " $anz " Ergebnisse, die nächsten 10 sind:" n

  Schleife Kurzanzeige
:schlk

var $LIST (b"showFullRecord" e'"' t"=")
ins _&amp;_&_
ins $co

wri z ". " 
var $LIST (b'>' e"</") 
ANSI
var (f" ")
ins $li
wri $li n

var $LIST
if not %Details zu diesem Datensatz% jump lend
  
var (b"Details zu diesem Datensatz")
ins $LIST
z+1

if $LIST jump schlk

:lend

var $LIST (b"method=showNextResultSite" e'"')
ins _&amp;_&_
ins $np

var $anz
if >10 wri "Die nächsten 10" n n
wri n n n n n n n "ABBRUCH" n
close x
  Auswahlliste anzeigen, ?1 heisst ANSI Code 
Aresqa ?1erg.lst

  Welche Zeile wurde gewaehlt?
if "" end

ins $zeile
  die letzte, zum Weiterschalten
if "Die n" jump Next

var $zeile
var (e".")
ins $nu
eval $nu -1
ins $nu
var $zeile (e" --")
 asci
 xcode iq 
ins $zeile

var n n "Rufe HTML-Daten ab für " $zeile 
sho +iV
var "Fhttps://portal.dnb.de/opac.htm?method=showFullRecord" $co "=" $nu

var

// Satz in HTML-Portalform ist in iV

jump anfang2

:Next
var n "Abruf weiterer Daten"
sho +iV
var "Fhttps://portal.dnb.de/opac.htm?method=showNextResultSite" $np

var

jump anfang



// Es war ein Irrtum
:forget
erase
var $idnr
find
sho rec
disp
set e1
end

// hier iV-Inhalt (HTML!) umwandeln: tab, cr, lf weg
:umwa

ins _^I__
ins _^M__
ins _^J__
ins _^T__
// Zeilenvorschuebe durch " -- " ersetzen
ins _<br/>_ -- _
ins _<br>_ --  _

ins _&amp;_&_
//  ins _&lt;_<_   nicht global, nur bei den Feldern, wo es auftreten kann, sonst andere Probl.

// einige Kombinationen kontrahieren
// treten manchmal auf (z.B. ä als Kombination aus a und Trema)
ins _u½__
ins _o½_”_
ins _a½_„_
ins _A½__
ins _O½_™_
ins _U½_š_

// Akzente ansonsten vertauschen von rechts nach links
dow Ai
return


