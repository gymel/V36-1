  PICAFILE.FLX : ASCII-Datei zeilenweise lesen + schreiben, dabei Manipulationen moeglich
  2001-02-08 : (Muster zum Variieren!)  Variante f. Pica Download
  Leerzeichen am Zeilenende verschwinden, am Anfang nicht!

var m
if "alc" end
  Zuerst eine Datei auswaehlen lassen
mess Dateiverarbeitung. Die ausgewählte Datei wird zeilenweise gelesen und geschrieben
fname Wählen Sie eine Datei:|*.*
    alternativ:  hier z.B. wuerden nur Dateien des Typs *.?pr gezeigt
  fnam Wählen Sie eine Datei|*.?pr

  den gewaehlten Namen in #uFN speichern
ins #uFN
  zum Lesen oeffnen (Name steht noch in iV)
open
:neuname
  Fragen, wie die neue Datei heissen soll
ask Neuer Name für die Datei?
if "" end
  den eingegebenen neuen Namen in #uNN speichern
ins #uNN
fsize
if no jump start
noyes Datei existiert! Ueberschreiben?
if not yes jump neuname
del
:start
  und zum Schreiben (Exportieren) oeffnen (Name steht noch in iV)
var #uNN
xport f
    wenn Exportparameter "name.cpr" zusaetzlich gebraucht werden:
  xport p name

  ZeilenZaehler (iz = interner Zaehler) auf 1 setzen (falls gewuenscht)

z = 1

  erste Zeile lesen (X davor setzen)
var "X"
get +iV

  ^^^^^^^^^^^^^^^^^ Beginn der Schleife
:loop
  gelesene Zeile (steht in iV) in #uZL speichern, mit X davor
  (sonst wuerden fuehrende Leerzeichen bei "ins #uZK" verschwinden...)

ins #uZL
  ***************************************

var #uZL(1,1)
if not "0" if not "1" if not "2" if not "3" if not "4" if not "5" if not "6" if not "7" if not "8" if not "8" if not "9" jump nxl

  NUR wenn erstes Zeichen Ziffer UND 5. Zeichen blank wird 13 10 geschrieben:

var #uZL(5,1)
if " " wri n

:nxl
wri #uZL(1,0)

  ***************************************
  Zaehler um 1 hochzaehlen
z + 1
  naechste Zeile lesen
var "X"
get +iV
  war noch eine Zeile da? dann Schleife
if yes jump loop
  ^^^^^^^^^^ Ende der Schleife

  Ende, Exportdatei schliessen (indem eine neue namens "nul" geoeffnet wird)
xport f nul
  internen Zaehler nach #uZZ kopieren:
var z
ins #uZZ
  Meldung aufbereiten und in #uME speichern
var #uFN " verarbeitet, Neue Datei " #uNN 13 10 #uZZ " Zeilen"
ins #uME

  Variablen loeschen
var ""
ins #uZL
ins #uZZ
ins #uFN

  EndeMeldung ausgeben (ins Schreibfeld)
var #uNN
fsize
ins #uNZ
var #uME ", " #uNZ " Bytes"

mess
 show iv

  Neue Datei anzeigen
var #uNN
help
var ""
ins #uNN
